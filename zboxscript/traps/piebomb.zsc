//zboxscript/traps/piebomb.zsc

CLASS PieBomb : BoxActor
{
	//it's a tripmine pie.
	
	Default
	{
		Health 10;
		Speed 0;
		Radius 14;
		Height 14;
		Mass 0x7FFFFFFF; //immovable object
		
		DamageFactor "Lemon", 0.1;
		DamageFactor "Boom", 1000.0;
		DamageFactor "Pie", 0.0;
		
		PainChance 0;
		
		+SHOOTABLE;
		+SOLID;
		+NEVERTARGET;
		+FRIGHTENING;
		+NOBLOODDECALS;
		+CASTSPRITESHADOW;
		//$Category "Box Party/Traps"
		//$Title "Pie Bomb"
		//$Color 5
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_Jump(128, "Looper");
			TNT1 A 0 {bSPRITEFLIP = true;}
			Goto Looper;
		Looper:
			PIEB A 1 A_LookEX(LOF_NOSOUNDCHECK, 0, 32, 1, 360, "Death");
			Loop;
		Death:
			PIEB A 0 A_DamageSelf(9999999); //ensure that the pie is fully dead before continuing
			PIEB B 2 BRIGHT Slow A_StartSound("traps/piebomb", 0, CHANF_OVERLAP);
			PIEB A 6 BRIGHT Fast;
			PIEB B 2 BRIGHT Slow A_StartSound("traps/piebomb", 0, CHANF_OVERLAP);
			PIEB A 6 BRIGHT Fast;
			PIEB B 2 BRIGHT Slow A_StartSound("traps/piebomb", 0, CHANF_OVERLAP);
			PIEB A 3 BRIGHT Fast;
			PIEB C 0 A_AlertMonsters(256);
			PIEB C 0 A_SpawnItemEX("DangerBoomPie", 0, 0, 14, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
			PIEB CCCCCCCCCCCC 0 A_SpawnItemEX("PieChunk", frandom(-8, 8), frandom(-8, 8), frandom(8, 20), frandom(-8, 8), frandom(-8, 8), frandom(-8, 8), 0,  SXF_NOCHECKPOSITION, 64);
			PIEB C 0 A_NoBlocking;
			Goto Smokey;
		Smokey:
			PIEB CCC 8 A_SpawnItemEX("BoomSmoke", 0, 0, 0, frandom(-1, 1), frandom(-1, 1), 4, 0, SXF_NOCHECKPOSITION, 64);
			PIEB C 0 A_Jump(224, "Smokey");
		Rest:
			PIEB C -1;
			Stop;
	}
}

CLASS PieChunk : BoxChunk
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PIEB D 0 A_Jump(128, 2);
			PIEB D 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS DangerBoomPie : DangerBoom {Default {DamageType "Pie";}}