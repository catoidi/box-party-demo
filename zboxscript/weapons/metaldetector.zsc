//zboxscript/weapons/metaldetector.zsc

CLASS MetalDetector : Weapon
{
	Default
	{
		Radius 20;
		Height 24;
		Weapon.SlotNumber 0;
		Weapon.SelectionOrder 9999;
		Inventory.PickupMessage "$BOX_METALDETECTOR";
		DamageType "MetalDetector";
		
		+WEAPON.WIMPY_WEAPON;
		+WEAPON.AMMO_OPTIONAL;
		+WEAPON.ALT_AMMO_OPTIONAL;
		+WEAPON.NOALERT;
		+INVENTORY.UNDROPPABLE;
		+WEAPON.CHEATNOTWEAPON;
		+CASTSPRITESHADOW;
		//$Category "Box Party/Weapons"
		//$Title "Fishing Rod"
		//$Color 9
	}
	
	States
	{
		Ready:
			METL A 1 A_WeaponReady;
			Loop;
		Deselect:
			METL AA 0 A_Lower;
			METL A 1 A_Lower;
			Loop;
		Select:
			METL AA 0 A_Raise;
			METL A 1 A_Raise;
			Loop;
		Fire:
			METL B 0 A_StartSound("weapons/laser/lasrpew");
			METL B 10 A_FireProjectile("MetalDetectorBullet", 0, 0, -4, 8, FPF_AIMATANGLE, 0);
			METL A 10;
			Goto Ready;
		Spawn:
			METL C -1;
			Stop;
	}
}

CLASS MetalDetectorBullet : BoxActor
{
	//the projectile that can shock enemies
	Default
	{
		Mass 0;
		Gravity 0;
		Radius 6;
		Height 12;
		Speed 10;
		Scale 0.25;
		DamageFunction (1);
		DamageType "MetalDetector";
		Projectile;
		-BLOODSPLATTER;
		+INTERPOLATEANGLES;
		+ROLLSPRITE;
		+SPRITEANGLE;
		+FORCEXYBILLBOARD;
	}
	
	States
	{
		Spawn:
			TNT1 A 0 A_SetRoll(random(0, 359));
		Looper:
			SLAM B 1;
			Loop;
		XDeath:
		Crash:
			SLAM B 0 A_StartSound("weapons/laser/lasrhit");
		Death.Sky:
		Death:
			SLAM BBBB 1
			{
				A_BoxScale(0.1);
				A_FadeOut(0.2);
			}
			Stop;
	}
}

CLASS MetalDetectorMarker : BoxActor
{
	Default
	{
		Health 1;
		Radius 16;
		Height 20;
		PainChance 256;
		PainChance "MetalDetector", 256;
		-SOLID;
		+NODAMAGE;
		+SHOOTABLE;
		+NOBLOOD;
	}
	
	States
	{
		Spawn:
			METL DE 35 BRIGHT;
			Loop;
		Pain:
			METL D 1;
			Goto Spawn;
		Pain.MetalDetector:
			METL D 1 A_Die();
			Goto Death;
		Death:
			TNT1 A 1
			{
				string spawnedy;
				for (int i = 1; i < 10; i++)
				{
					A_SpawnItemEx("MetalDetectorChunk", random(-6, 6), random(-6, 6), random(0, 8), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), 0, 128);
				}
				
				if (random(1, 10) == 10)
				{
					spawnedy = "FruitPuncher_Tiny";
				}
				else if (random(1, 2) == 2)
				{
					spawnedy = "MetalDetectorCoin";
				}
				else
				{
					spawnedy = "MetalDetectorTrash";
				}
				A_SpawnItemEx(spawnedy, random(-6, 6), random(-6, 6), random(0, 8), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), SXF_NOCHECKPOSITION);
			}
			Stop;
	}
}

CLASS MetalDetectorChunk : BoxChunk
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			METL F 0 A_Jump(128, 2);
			METL F 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

class MetalDetectorCoin : BoxQuestReward
{
	Default
	{
		Inventory.Amount 100;
		+FLOATBOB;
		+BRIGHT;
	}
	
	States
	{
		Spawn:
			METL G -1;
			Loop;
	}
}

CLASS MetalDetectorTrash : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			TRSH A 0 {frame = random(0, 13);}
			"####" "#" 0 A_Jump(128, 2);
			"####" "#" 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}