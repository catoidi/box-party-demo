//zboxscript/weapons/glowstick.zsc

//zscript code for the glowstick and glowstick accessories.

CLASS GlowstickPower : Inventory
{
	//this controls how long the glowstick's glow lasts.
	//one power is removed per tick, and there are 35 ticks per second.
	//the glowstick can last for a total of 10 seconds before running out.
	//the glowstick gains 70 power per swing, or 2 seconds of glow.

	Default
	{
		Inventory.MaxAmount 350;
	}
}

CLASS GlowstickIsFiring : Inventory
{
	//this makes the player sprite use an alternate melee attack instead of the regular attack sprite.
	
	Default
	{
		Inventory.MaxAmount 1;
	}
}

CLASS Glowstick : Weapon //20010
{
	Default
	{
		Radius 20;
		Height 24;
		Weapon.SelectionOrder 9999;
		Weapon.Kickback 500;
		Weapon.AmmoUse1 1;
		Weapon.AmmoGive1 1;
		Weapon.AmmoType1 "GlowstickAmmo";
		Inventory.PickupMessage "$BOX_WEAPON_GLOW";
		Inventory.Icon "AMMOH0";
		Inventory.AltHUDIcon "AMMOA0";
		Obituary "$BOX_DIE_GLOW";
		Tag "$BOX_TAG_GLOW";
		DamageType "Melee";
		
		+BLOODSPLATTER;
		+WEAPON.MELEEWEAPON;
		+WEAPON.WIMPY_WEAPON;
		+WEAPON.AMMO_OPTIONAL;
		+WEAPON.ALT_AMMO_OPTIONAL;
		+WEAPON.NOALERT;
		+INVENTORY.UNDROPPABLE;
		+CASTSPRITESHADOW;
		+COUNTITEM;
		//$Category "Box Party/Weapons"
		//$Title "Glowstick (Weapon)"
		//$Color 9
	}
	
	States
	{
		Ready:
			//the ready states act finicky with sv_fastweapons on.
			//so if youre gonna have some kind of haste powerup, make a workaround for the glowstick.
			GLOW L 0 A_JumpIfInventory("GlowstickPower", 1, "ReadyGlow");
			GLOW L 0 A_RemoveLight("Glower");
			GLOW L 1 A_WeaponReady;
			Loop;
		ReadyGlow:
			GLOW B 0 BRIGHT A_AttachLightDef("Glower", "GLOWGREENBIG");
			GLOW B 0 BRIGHT A_TakeInventory("GlowstickPower", 1);
			GLOW B 1 BRIGHT A_WeaponReady;
			GLOW B 0 BRIGHT A_JumpIfInventory("GlowstickPower", 1, "ReadyGlow");
			Goto Ready;
		Deselect:
			TNT1 A 0 A_TakeInventory("GlowstickPower");
			GLOW L 0 A_RemoveLight("Glower");
			GLOW LL 0 A_Lower;
			GLOW L 1 A_Lower;
			Loop;
		Select:
			GLOW LL 0 A_Raise;
			GLOW L 1 A_Raise;
			Loop;
		Fire:
		//the glowstick does does a swipe attack coming from the top left and going to the bottom right.
			TNT1 A 0 A_AttachLightDef("Glower", "GLOWGREENBIG");
			TNT1 A 0 A_GiveInventory("GlowstickIsFiring", 1);
			GLOW B 1 BRIGHT {
				A_WeaponOffset(30, 20, WOF_INTERPOLATE); 
				A_GiveInventory("GlowstickPower", 35);
			}
			GLOW B 1 BRIGHT A_WeaponOffset(-60, -40, WOF_INTERPOLATE);
			GLOW B 0 BRIGHT A_StartSound("weapons/glowstick/glowswsh");
			GLOW BBB 1 BRIGHT A_WeaponOffset(30, 20, WOF_ADD | WOF_INTERPOLATE);
			GLOW N 1 BRIGHT {
				A_WeaponOffset(60, 40, WOF_INTERPOLATE);
				A_CustomPunch(32, true, CPF_PULLIN, "ProjectileSmoke", 96, 0, 0, "ArmorBonus", "weapons/glowstick/glowbonk");
			}
			GLOW BBBBB 1 BRIGHT A_WeaponOffset(30, 20, WOF_ADD | WOF_INTERPOLATE);
			GLOW C 3 BRIGHT A_WeaponOffset(5, 40, WOF_INTERPOLATE);
			GLOW B 3 BRIGHT A_Refire("Fire");
			GLOW B 0 BRIGHT A_TakeInventory("GlowstickIsFiring", 1);
			Goto Ready;
		AltFire:
			//checks if you have enough ammo to throw glowstick.
			//if not, go to melee attack.
			TNT1 A 0 A_AttachLightDef("Glower", "GLOWGREENBIG");
			GLOW B 1 BRIGHT A_JumpIfInventory("GlowstickAmmo", 1, "YouGotIt");
			Goto Fire;
		YouGotIt:
			//successful glowstick throwing
			GLOW B 0 BRIGHT A_StartSound("weapons/glowstick/glowswsh");
			GLOW BBBB 1 BRIGHT A_WeaponOffset(20, 20, WOF_ADD | WOF_INTERPOLATE);
			TNT1 A 0 A_RemoveLight("Glower");
			TNT1 A 0 A_TakeInventory("GlowstickPower");
			TNT1 A 0 A_TakeInventory("GlowstickAmmo", 1);
			TNT1 A 19 A_FireProjectile("GlowstickThrow", 0, 0, 0, 4, FPF_AIMATANGLE, frandom(-4, 4));
			TNT1 A 1 A_WeaponOffset(5, 120);
			GLOW L 10 A_WeaponOffset(5, 80, WOF_INTERPOLATE);
			GLOW L 10 {
				A_WeaponOffset(5, 40, WOF_INTERPOLATE);
				A_ReFire("AltFire");
			}
			Goto Ready;
		Spawn:
			//this item should only appear once in the world. at the very beginning.
			GLOW A -1 BRIGHT;
			Stop;
	}
}

CLASS GlowstickAmmo : Ammo //20021
{
	//extra glowsticks for throwin.
	//there should be lots of these in the world.
	Default
	{
		Radius 20;
		Height 24;
		Inventory.PickupMessage "$BOX_AMMO_GLOW";
		Inventory.Amount 1;
		Inventory.MaxAmount 4;
		Inventory.Icon "GLOWA0";
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 8;
		+CASTSPRITESHADOW;
		+COUNTITEM;
		//$Category "Box Party/Ammo"
		//$Title "Glowstick (Ammo)"
		//$Color 19
	}
	
	States
	{
		Spawn:
			GLOW K -1;
			Stop;
	}
}

CLASS GlowstickAmmoDrop : GlowstickAmmo
{
	mixin BoxActorMixin;

	Default
	{
		+FLOATBOB;
		-COUNTITEM;
	}
	
	int FadeNum;

	States
	{
		Spawn:
			LOOT AAABBBAAABBBAAABBB 10 BRIGHT A_BoxParticle2("ff ec 42", random(1, 2), 12, 24, POffset: 8.0, POffsetZ: frandom(0.0, 4.0), PSpeed: 2.0, PBright: true);
			#### # 0
			{
				FadeNum += 1;
				if (FadeNum == 2) {A_BoxParticle("ff ec 42", 8, 12, 24);}
			}
			LOOT A 0 A_FadeOut(0.5);
			Loop;
	}
}

CLASS GlowstickThrow : Actor
{
	//the thing that you throw with your secondary fire.
	//can be used to glow and to kill.
	//each glowstick will be one of the six random colors, and eventually run out to help performance.
	Default
	{
		DamageFunction (80);
		DamageType "Melee";
		ProjectileKickback 1000;
		Speed 25;
		Radius 6;
		Height 4;
		Gravity 0.3;
		Mass 1;
	
		
		Projectile;
		-NOGRAVITY;
		+INTERPOLATEANGLES;
		+ROLLSPRITE;
		+SPRITEANGLE;
		+FORCEXYBILLBOARD;
		+BLOODSPLATTER;
	}
	
	States
	{
		Spawn:
			GLOW J 0 NoDelay A_SetRoll(random(0, 359));
			GLOW J 0 A_Jump(256, "Firsty", "Secondy", "Thirdy", "Fourthy", "Fifthy", "Sixthy", "Seventhy");
			GLOW A 100000;
			Stop;
		Firsty:
			GLOW D 1 BRIGHT;
			Goto SpeenStart;
		Secondy:
			GLOW E 1 BRIGHT;
			Goto SpeenStart;
		Thirdy:
			GLOW F 1 BRIGHT;
			Goto SpeenStart;
		Fourthy:
			GLOW G 1 BRIGHT;
			Goto SpeenStart;
		Fifthy:
			GLOW H 1 BRIGHT;
			Goto SpeenStart;
		Sixthy:
			GLOW I 1 BRIGHT;
			Goto SpeenStart;
		Seventhy:
			GLOW J 1 BRIGHT;
			Goto SpeenStart;
		SpeenStart:
			GLOW "#" 1 BRIGHT A_Jump(128, "SpeenLeft");
		SpeenRight:
			"####" "#" 1 BRIGHT A_SetRoll(roll - 8);
			Loop;
		SpeenLeft:
			"####" "#" 1 BRIGHT A_SetRoll(roll + 8);
			Loop;
		XDeath:
		Crash:
			"####" "#" 0 BRIGHT A_StartSound("weapons/glowstick/glowwack");
		Death.Sky:
		Death:
			"####" "#" 1 BRIGHT A_SetRoll(0);
			"####" "#" 0 BRIGHT {bMOVEWITHSECTOR = true;}
			"####" "#" 0 BRIGHT A_SpawnItemEX("ProjectileSmokeBig", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
			"####" "#" 0 BRIGHT A_Jump(256, "Flipper", "Stopper");
		Flipper:
			"####" "#" 0 BRIGHT {bSPRITEFLIP = TRUE;}
		Stopper:
			"####" "#" 1000 BRIGHT;
			"####" "#" 0 A_QueueCorpse;
			"####" K 0 A_FadeOut(0.5);
		Burnout:
			"####" K 250 A_FadeOut(0.1);
			Loop;
	}
}