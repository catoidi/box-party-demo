//zboxscript/weapons/glowstick.zsc

//zscript code for the glowstick and glowstick accessories.

CLASS GlowstickPower : Inventory
{
	//this controls how long the glowstick's glow lasts.
	//one power is removed per tick, and there are 35 ticks per second.
	//the glowstick can last for a total of 10 seconds before running out.
	//the glowstick gains 70 power per swing, or 2 seconds of glow.

	Default
	{
		Inventory.MaxAmount 350;
	}
}

CLASS GlowstickIsFiring : Inventory
{
	//this makes the player sprite use an alternate melee attack instead of the regular attack sprite.
	
	Default
	{
		Inventory.MaxAmount 1;
	}
}

CLASS GlowstickColor : Inventory
{
	//defines which color will be used for the current glowstick.
	//green equals 0.
	Default
	{
		Inventory.MaxAmount 6;
	}
}

CLASS Glowstick : Weapon //20010
{
	Default
	{
		Radius 20;
		Height 24;
		Weapon.SelectionOrder 9999;
		Weapon.Kickback 500;
		Weapon.AmmoUse1 1;
		Weapon.AmmoGive1 1;
		Weapon.AmmoType1 "GlowstickAmmo";
		Inventory.PickupMessage "$BOX_WEAPON_GLOW";
		Inventory.Icon "AMMOH0";
		Inventory.AltHUDIcon "AMMOA0";
		Obituary "$BOX_DIE_GLOW";
		Tag "$BOX_TAG_GLOW";
		DamageType "Melee";
		+BLOODSPLATTER;
		+WEAPON.MELEEWEAPON;
		+WEAPON.WIMPY_WEAPON;
		+WEAPON.AMMO_OPTIONAL;
		+WEAPON.ALT_AMMO_OPTIONAL;
		+WEAPON.NOALERT;
		+INVENTORY.UNDROPPABLE;
		+CASTSPRITESHADOW;
		+COUNTITEM;
		//$Category "Box Party/Weapons"
		//$Title "Glowstick (Weapon)"
		//$Color 9
	}
	
	string CurTrans;
	string CurLight;
	string CurThrow;
	
	//change the color of the glowstick's weapon sprite to match the next thrown projectile.
	action void ChangeGlowColor()
	{
		A_SetInventory("GlowstickColor", random(0, 6));
	
		CheckGlowColor();
	}
	
	action void CheckGlowColor()
	{
		switch (CountInv("GlowstickColor"))
		{
			default: invoker.CurTrans = "GreenGlow";  invoker.CurLight = "GLOWGREENBIG"; invoker.CurThrow = ""; break;
			case 1: invoker.CurTrans = "BlueGlow"; invoker.CurLight = "GLOWBLUEBIG"; invoker.CurThrow = "Blue"; break;
			case 2: invoker.CurTrans = "RedGlow"; invoker.CurLight = "GLOWREDBIG2"; invoker.CurThrow = "Red"; break;
			case 3: invoker.CurTrans = "YellowGlow"; invoker.CurLight = "GLOWYELLOWBIG"; invoker.CurThrow = "Yellow"; break;
			case 4: invoker.CurTrans = "PinkGlow"; invoker.CurLight = "GLOWPINKBIG"; invoker.CurThrow = "Pink"; break;
			case 5: invoker.CurTrans = "CyanGlow"; invoker.CurLight = "GLOWAQUABIG2"; invoker.CurThrow = "Cyan"; break;
			case 6: invoker.CurTrans = "WhiteGlow"; invoker.CurLight = "GLOWWHITEBIG"; invoker.CurThrow = "White"; break;
		}
		
		A_OverlayTranslation(1, invoker.CurTrans);
		A_OverlayTranslation(2, invoker.CurTrans);
	}
	
	States
	{	
		Ready:
			GLOW L 1
			{
				A_WeaponReady();
				if (CheckInventory("GlowstickPower", 1)) 
				{
					A_Overlay(2, "Glowverlay");
					CheckGlowColor();
				}
				else 
				{
					A_RemoveLight("Glower"); 
					A_Overlay(2, null);
				}
			}
			Loop;
		Glowverlay:
			GLOW B 0 BRIGHT A_AttachLightDef("Glower", invoker.CurLight);
			GLOW B 1 BRIGHT A_TakeInventory("GlowstickPower", 1);
			Stop;
		Deselect:
			TNT1 A 0 
			{
				A_TakeInventory("GlowstickPower");
				A_RemoveLight("Glower");
				
				//im doing this so that the glowstick's translations doesn't leak onto other weapons.
				//something in my gut is telling me that deleting the main weapon layer and recreating it on the same frame is a bad idea,
				//but from my testing everything seems to be working fine, so idk.
				//this will probably mess up if the glowstick item is taken away before the overlays can be updated.
				A_ClearOverlays(1, 2, false);
				A_Overlay(1, "Lowering");
			}
		Lowering:
			GLOW LL 0 A_Lower;
			GLOW L 1 A_Lower;
			Loop;
		Select:
			GLOW LL 0 A_Raise;
			GLOW L 1 A_Raise;
			Loop;
		Fire:
		//the glowstick does does a swipe attack coming from the top left and going to the bottom right.
			TNT1 A 0 A_AttachLightDef("Glower", invoker.CurLight);
			TNT1 A 0 A_GiveInventory("GlowstickIsFiring", 1);
			TNT1 A 0 CheckGlowColor();
			GLOW B 1 BRIGHT {
				A_WeaponOffset(30, 20, WOF_INTERPOLATE); 
				A_GiveInventory("GlowstickPower", 35);
			}
			GLOW B 1 BRIGHT A_WeaponOffset(-60, -40, WOF_INTERPOLATE);
			GLOW B 0 BRIGHT A_StartSound("weapons/glowstick/glowswsh");
			GLOW BBB 1 BRIGHT A_WeaponOffset(30, 20, WOF_ADD | WOF_INTERPOLATE);
			GLOW N 1 BRIGHT {
				A_WeaponOffset(60, 40, WOF_INTERPOLATE);
				A_CustomPunch(32, true, CPF_PULLIN, "ProjectileSmoke", 96, 0, 0, "ArmorBonus", "weapons/glowstick/glowbonk");
			}
			GLOW BBBBB 1 BRIGHT A_WeaponOffset(30, 20, WOF_ADD | WOF_INTERPOLATE);
			GLOW C 3 BRIGHT A_WeaponOffset(5, 40, WOF_INTERPOLATE);
			GLOW B 3 BRIGHT A_Refire("Fire");
			GLOW B 0 BRIGHT A_TakeInventory("GlowstickIsFiring", 1);
			Goto Ready;
		AltFire:
			//checks if you have enough ammo to throw glowstick.
			//if not, go to melee attack.
			TNT1 A 0 A_AttachLightDef("Glower", invoker.CurLight);
			GLOW B 1 BRIGHT A_JumpIfInventory("GlowstickAmmo", 1, "YouGotIt");
			Goto Fire;
		YouGotIt:
			//successful glowstick throwing
			GLOW B 0 BRIGHT A_StartSound("weapons/glowstick/glowswsh");
			GLOW BBBB 1 BRIGHT A_WeaponOffset(20, 20, WOF_ADD | WOF_INTERPOLATE);
			TNT1 A 0 A_RemoveLight("Glower");
			TNT1 A 0 A_TakeInventory("GlowstickPower");
			TNT1 A 0 A_TakeInventory("GlowstickAmmo", 1);
			TNT1 A 19 
			{
				string glowthrower = "GlowstickThrow";
				glowthrower.AppendFormat(invoker.CurThrow);
				A_FireProjectile(glowthrower, 0, 0, 0, 4, FPF_AIMATANGLE, frandom(-4, 4));
			}
			TNT1 A 1 A_WeaponOffset(5, 120);
			#### # 0 ChangeGlowColor();
			GLOW L 10 A_WeaponOffset(5, 80, WOF_INTERPOLATE);
			GLOW L 10 {
				A_WeaponOffset(5, 40, WOF_INTERPOLATE);
				A_ReFire("AltFire");
			}
			Goto Ready;
		Spawn:
			//this item should only appear once in the world. at the very beginning.
			GLOW A -1 BRIGHT;
			Stop;
	}
}

CLASS GlowstickAmmo : Ammo //20021
{
	//extra glowsticks for throwin.
	//there should be lots of these in the world.
	Default
	{
		Radius 20;
		Height 24;
		Inventory.PickupMessage "$BOX_AMMO_GLOW";
		Inventory.Amount 1;
		Inventory.MaxAmount 4;
		Inventory.Icon "GLOWA0";
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 8;
		+CASTSPRITESHADOW;
		+COUNTITEM;
		//$Category "Box Party/Ammo"
		//$Title "Glowstick (Ammo)"
		//$Color 19
	}
	
	States
	{
		Spawn:
			GLOW K -1;
			Stop;
	}
}

CLASS GlowstickAmmoDrop : GlowstickAmmo
{
	mixin BoxActorMixin;

	Default
	{
		+FLOATBOB;
		-COUNTITEM;
	}
	
	int FadeNum;

	States
	{
		Spawn:
			LOOT AAABBBAAABBBAAABBB 10 BRIGHT A_BoxParticle2("ff ec 42", random(1, 2), 12, 24, POffset: 8.0, POffsetZ: frandom(0.0, 4.0), PSpeed: 2.0, PBright: true);
			#### # 0
			{
				FadeNum += 1;
				if (FadeNum == 2) {A_BoxParticle("ff ec 42", 8, 12, 24);}
			}
			LOOT A 0 A_FadeOut(0.5);
			Loop;
	}
}

CLASS GlowstickThrow : Actor
{
	//the thing that you throw with your secondary fire.
	//can be used to glow and to kill.
	//each glowstick will be one of the six random colors, and eventually run out to help performance.
	
	
	int GNum;
	Property GColor: GNum;
	
	Default
	{
		DamageFunction (80);
		DamageType "Melee";
		ProjectileKickback 1000;
		Speed 25;
		Radius 6;
		Height 4;
		Gravity 0.3;
		Mass 1;
		GlowstickThrow.GColor 0;
		
		Projectile;
		-NOGRAVITY;
		+INTERPOLATEANGLES;
		+ROLLSPRITE;
		+SPRITEANGLE;
		+FORCEXYBILLBOARD;
		+BLOODSPLATTER;
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			Goto Coloring;
		Coloring:
			GLOW D 1 BRIGHT 
			{
				int gframe = 3 + self.GNum;
				frame = gframe;
				
				string glight;
				switch (self.GNum)
				{
					default: glight = "GLOWGREEN"; break;
					case 1: glight = "GLOWBLUE"; break;
					case 2: glight = "GLOWRED"; break;
					case 3: glight = "GLOWYELLOW"; break;
					case 4: glight = "GLOWPINK"; break;
					case 5: glight = "GLOWAQUA"; break;
					case 6: glight = "GLOWWHITE"; break;
				}
				A_AttachLightDef("sticker", glight);
			}
			Goto SpeenStart;
		SpeenStart:
			#### # 1 BRIGHT A_Jump(128, "SpeenLeft");
		SpeenRight:
			"####" "#" 1 BRIGHT A_SetRoll(roll - 8);
			Loop;
		SpeenLeft:
			"####" "#" 1 BRIGHT A_SetRoll(roll + 8);
			Loop;
		XDeath:
		Crash:
			"####" "#" 0 BRIGHT A_StartSound("weapons/glowstick/glowwack");
		Death.Sky:
		Death:
			"####" "#" 1 BRIGHT A_SetRoll(0);
			"####" "#" 0 BRIGHT {bMOVEWITHSECTOR = true;}
			"####" "#" 0 BRIGHT A_SpawnItemEX("ProjectileSmokeBig", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
			"####" "#" 0 BRIGHT A_Jump(256, "Flipper", "Stopper");
		Flipper:
			"####" "#" 0 BRIGHT {bSPRITEFLIP = TRUE;}
		Stopper:
			"####" "#" 1000 BRIGHT;
			"####" "#" 0 A_QueueCorpse;
			"####" "#" 0 A_RemoveLight("sticker");
			"####" K 0 A_FadeOut(0.5);
		Burnout:
			"####" K 250 A_FadeOut(0.1);
			Loop;
	}
}

Class GlowstickThrowBlue : GlowstickThrow {Default {GlowstickThrow.GColor 1;}}
Class GlowstickThrowRed : GlowstickThrow {Default {GlowstickThrow.GColor 2;}}
Class GlowstickThrowYellow : GlowstickThrow {Default {GlowstickThrow.GColor 3;}}
Class GlowstickThrowPink : GlowstickThrow {Default {GlowstickThrow.GColor 4;}}
Class GlowstickThrowCyan : GlowstickThrow {Default {GlowstickThrow.GColor 5;}}
Class GlowstickThrowWhite : GlowstickThrow {Default {GlowstickThrow.GColor 6;}}