//zboxscript/weapons/popcorngun.zsc

CLASS PopcornGunPower : Inventory {Default{Inventory.MaxAmount 3;}}

CLASS PopcornGunAmmo : Ammo //200
{
	Default
	{
		Radius 20;
		Height 24;
		Inventory.PickupMessage "$BOX_AMMO_POPCORN";
		Inventory.Amount 20;
		Inventory.MaxAmount 80;
		Inventory.Icon "POPGZ0";
		Ammo.BackpackAmount 10;
		Ammo.BackpackMaxAmount 120;
		+CASTSPRITESHADOW;
		//popcorngun shoot many popcorn, but not have many popcorn
		
		//$Category "Box Party/Ammo"
		//$Title "Popcorn Ammo"
		//$Color 19
	}
	
	States
	{
		Spawn:
			POPG Z -1;
			Stop;
	}
}

CLASS PopcornGunAmmo2 : Ammo
{
	//you gain 1 of this every time u fire primary.

	Default
	{
		Inventory.Amount 1;
		Inventory.MaxAmount 40;
		Inventory.Icon "AMMOK0";
		Inventory.AltHUDIcon "AMMOD0";
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 60;
	}

	States
	{
		//not supposed to show up naturally in the world.
		Spawn:
			PBOX A -1;
			Stop;
	}
}

CLASS PopcornGunAmmoDrop : PopcornGunAmmo
{
	mixin BoxActorMixin;

	Default
	{
		+FLOATBOB;
	}
	
	int FadeNum;

	States
	{
		Spawn:
			LOOT GGGHHHGGGHHHGGGHHH 10 BRIGHT A_BoxParticle2("ff ec 42", random(1, 2), 12, 24, POffset: 8.0, POffsetZ: frandom(0.0, 4.0), PSpeed: 2.0, PBright: true);
			#### # 0
			{
				FadeNum += 1;
				if (FadeNum == 2) {A_BoxParticle("ff ec 42", 8, 12, 24);}
			}
			LOOT G 0 A_FadeOut(0.5);
			Loop;
	}
}

CLASS PopcornGun : Weapon //200
{
	//popcorngun shoot very fast boolet

	Default
	{
		Radius 20;
		Height 24;
		Weapon.SelectionOrder 2000;
		Weapon.Kickback 100;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoUse2 20;
		Weapon.AmmoGive2 0;
		Weapon.AmmoType "PopcornGunAmmo";
		Weapon.AmmoType2 "PopcornGunAmmo2";
		Inventory.PickupMessage "$BOX_WEAPON_POPCORN";
		Inventory.Icon "AMMOL0";
		Inventory.AltHUDIcon "AMMOE0";
		Obituary "$BOX_DIE_POPCORN";
		Tag "$BOX_TAG_POPCORN";
		+WEAPON.AMMO_CHECKBOTH;
		+CASTSPRITESHADOW;
		//$Category "Box Party/Weapons"
		//$Title "Popcorn Gun"
		//$Color 9
	}
	
	int FireRate;
	
	action void A_PopcornGunFire (int CornOffset)
	{
		A_StartSound("weapons/popcorn/popcorn");
		A_FireProjectile("PopcornBullet", frandom(-CornOffset, CornOffset), 1, -4, 8, FPF_AIMATANGLE, 0);
		A_GiveInventory("PopcornGunAmmo2", 1);
	}
	
	States
	{
		Ready:
			POPG A 1 A_WeaponReady;
			Loop;
		Deselect:
			POPG A 0 A_Lower;
			POPG A 1 A_Lower;
			Loop;
		Select:
			POPG A 0 A_Raise;
			POPG A 1 A_Raise;
			Loop;
		Fire:
			POPG B 0 A_JumpIfInventory("PopcornGunAmmo", 1, 2);
			POPG B 0 A_Jump(256, "DontGotIt");
			POPG B 0 A_WeaponOffset(-10, 30, WOF_INTERPOLATE);
			#### # 0 A_JumpIfInventory("PopcornGunAmmo", 1, 3);
			#### # 0 ACS_NamedExecute("noammo", 0, 0, 0, 0);
			#### # 0 A_Jump(256, 2);
			POPG B 1 A_PopcornGunFire(0);
			POPG C 5 A_WeaponOffset(-5, 25, WOF_INTERPOLATE);
			POPG B 0 A_WeaponOffset(-10, 30, WOF_INTERPOLATE);
			#### # 0 A_JumpIfInventory("PopcornGunAmmo", 1, 2);
			#### # 0 A_Jump(256, 2);
			POPG B 1 A_PopcornGunFire(0);
			POPG C 5 A_WeaponOffset(-5, 25, WOF_INTERPOLATE);
			POPG C 3 A_Refire("FireTwo");
			Goto Ready;
		FireTwo:
			POPG D 0 A_WeaponOffset(-12, 32, WOF_INTERPOLATE);
			#### # 0 A_JumpIfInventory("PopcornGunAmmo", 1, 2);
			#### # 0 A_Jump(256, 2);
			POPG D 1 A_PopcornGunFire(2);
			POPG E 3 A_WeaponOffset(-7, 27, WOF_INTERPOLATE);
			POPG D 0 A_WeaponOffset(-12, 32, WOF_INTERPOLATE);
			#### # 0 A_JumpIfInventory("PopcornGunAmmo", 1, 2);
			#### # 0 A_Jump(256, 2);
			POPG D 1 A_PopcornGunFire(2);
			POPG E 3 A_WeaponOffset(-7, 27, WOF_INTERPOLATE);
			POPG D 0 A_Refire("FireTwo");
			POPG E 5 A_WeaponOffset(-5, 25, WOF_INTERPOLATE);
			POPG I 8;
			POPG J 8;
			Goto Ready;
		//FireThree has been removed.
		AltFire:
			POPN F 0;
			POPN F 0 A_JumpIfInventory("PopcornGunAmmo2", 20, 2);
			POPN F 0 A_Jump(256, "DontGotIt");
			POPN F 0 A_WeaponOffset(-14, 34, WOF_INTERPOLATE);
			POPN F 0 A_StartSound("weapons/popcorn/poppop");
			POPN F 4 A_FireProjectile("PopcornBomb", 0, 1, -4, 8, FPF_AIMATANGLE, 0);
			POPN H 8 A_WeaponOffset(-5, 25, WOF_INTERPOLATE);
			POPN IJ 8;
			POPN AAAAAAAA 1 A_WeaponOffset(0, 10, WOF_ADD | WOF_INTERPOLATE);
			POPG AAAAAAAA 1 A_WeaponOffset(0, -10, WOF_ADD | WOF_INTERPOLATE);
			Goto Ready;
		DontGotIt:
			POPG A 0 A_FireProjectile("PartyShottySmoke", 0, 0, -4, 8, FPF_AIMATANGLE, 0);
			#### # 0 ACS_NamedExecute("noammo", 0, 0, 0, 0);
			POPG A 1 A_WeaponOffset(-10, 30, WOF_INTERPOLATE);
			POPG A 5 A_WeaponOffset(-5, 25, WOF_INTERPOLATE);
			Goto Ready;
		Spawn:
			POPG M -1;
			Stop;
	}
}

CLASS PopcornBullet : BoxActor
{
	//pew epw ewppewp
	Default
	{
		DamageFunction (15);
		DamageType "Popcorn";
		BounceType "Doom";
		BounceFactor 0.4;
		WallBounceFactor 0.8;
		ProjectileKickback 100;
		Speed 30;
		Radius 4;
		Height 4;
		Gravity 0.2;
		Mass 1;
	
		
		Projectile;
		-NOGRAVITY;
		+INTERPOLATEANGLES;
		+ROLLSPRITE;
		+SPRITEANGLE;
		+FORCEXYBILLBOARD;
		+BLOODSPLATTER;
		+DONTBOUNCEONSHOOTABLES;
		+DONTBOUNCEONSKY;
		+EXPLODEONWATER;
	}
	
	States
	{
		Spawn:
			POPG K 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 1 A_Jump(128, "SpeenLeft");
		SpeenRight:
			"####" "#" 1 A_SetRoll(roll - 8);
			Loop;
		SpeenLeft:
			"####" "#" 1 A_SetRoll(roll + 8);
			Loop;
		XDeath:
		Crash:
			TNT1 A 0 A_StartSound("weapons/popcorn/pophit");
		Death.Sky:
		Death:
			TNT1 A 1 {
				A_SpawnItemEx("ProjectileSmoke", 0, 0, 0, 0, 0, 0);
				A_BoxParticle("f6 f4 d9", 3);
				A_SpawnItemEx("PopcornBulletCrumb", random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 1), random(0, 359), 0, 192);
			}
			Stop;
	}
}

CLASS PopcornBulletCrumb : BoxCrumb
{
	//popcorn pellet breaks into 3 crumbs upon death.

	Default
	{
		Scale 0.75;
	}

	States
	{
		Spawn:
			POPG L 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS PopcornBomb : BoxActor
{
	Default
	{
		DamageFunction (128);
		DamageType "Popcorn";
		ProjectileKickback 300;
		Speed 30;
		Radius 8;
		Height 12;
		Gravity 0.4;
		Mass 10;

		Projectile;
		-NOGRAVITY;
		+INTERPOLATEANGLES;
		+ROLLSPRITE;
		+SPRITEANGLE;
		+FORCEXYBILLBOARD;
		+BLOODSPLATTER;
	}

	int CrumbTimer;
	int BulletAmount;

	States
	{
		Spawn:
			TNT1 A 0 NoDelay {
				A_SetRoll(random(0, 359));
				CrumbTimer = 0;
			}
			POPN K 1 A_Jump(128, "SpeenLeft");
		SpeenRight:
			"####" "#" 1 {
				A_SetRoll(roll - 8);
				if (CrumbTimer > 32) {CrumbTimer = 28;}
				A_SpawnItemEx("PopcornBombCrumb", frandom(-4, 4), frandom(-4, 4), frandom(-4, 4), 0, 0, 0, 0, SXF_ABSOLUTEVELOCITY, (224 - CrumbTimer));
				if (CrumbTimer < 32) {CrumbTimer += 4;}
			}
			Loop;
		SpeenLeft:
			"####" "#" 1 {
				A_SetRoll(roll + 8);
				if (CrumbTimer >= 32) {CrumbTimer = 28;}
				A_SpawnItemEx("PopcornBombCrumb", frandom(-4, 4), frandom(-4, 4), frandom(-4, 4), 0, 0, 0, 0, SXF_ABSOLUTEVELOCITY, (224 - CrumbTimer));
				if (CrumbTimer < 32) {CrumbTimer += 4;}
			}
			Loop;
		XDeath:
		Crash:
			TNT1 A 0 A_StartSound("weapons/popcorn/pophit");
		Death.Sky:
		Death:
			TNT1 A 4 {
				BulletAmount = 0;
				while (BulletAmount < 20)
				{
					A_SpawnItemEx("PopcornBullet", frandom(-4, 4), frandom(-4, 4), frandom(-4, 4), frandom(-30, 30), frandom(-30, 30), frandom(-12, 12), random(0, 359));
					BulletAmount += 1;
				}
				A_SpawnItemEx("SafeBoomSmall", 0, 0, 0, 0, 0, 0);
				A_Explode(12, 48);
				A_BoxParticle("cf 5f 04", 9, 8, 24);
				A_SpawnItemEx("PopcornBombCrumb", random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 1), random(0, 359));
				A_SpawnItemEx("PopcornBombCrumb", random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 1), random(0, 359));
				A_SpawnItemEx("PopcornBombCrumb", random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 1), random(0, 359));
			}
			TNT1 AAA 4 A_Explode(12, 48);
			Stop;
	}
}

CLASS PopcornBombCrumb : BoxCrumb
{
	Default
	{
		Scale 1;
	}

	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			POPN L 0 A_QueueCorpse;
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}






