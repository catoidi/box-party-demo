//zboxscript/weapons/fishingrod.zsc

CLASS FishingRod : Weapon //20010
{
	Default
	{
		Radius 20;
		Height 24;
		Weapon.SlotNumber 0;
		Weapon.SelectionOrder 9999;
		Inventory.PickupMessage "$BOX_FISHINGROD";
		Tag "$BOX_TAG_FISHINGROD";
		DamageType "FishingRod";
		
		+WEAPON.WIMPY_WEAPON;
		+WEAPON.AMMO_OPTIONAL;
		+WEAPON.ALT_AMMO_OPTIONAL;
		+WEAPON.NOALERT;
		+INVENTORY.UNDROPPABLE;
		+CASTSPRITESHADOW;
		//$Category "Box Party/Quest/"
		//$Title "Fishing Rod"
		//$Color 9
	}
	
	int fishtimer;
	
	States
	{
		Ready:
			FROD A 1 A_WeaponReady;
			Loop;
		Deselect:
			FROD AA 0 A_Lower;
			FROD A 1 A_Lower;
			Loop;
		Select:
			FROD AA 0 A_Raise;
			FROD A 1 A_Raise;
			Loop;
		Fire:
			FROD # 0 {ACS_NamedExecute("selffreezer", 0, 1, 0, 0);}
			FROD BB 10 A_WeaponOffset(0, 5, WOF_ADD | WOF_INTERPOLATE);
			FROD C 0 A_SpawnItemEX("FishingHook", 8, 0, 24, 8, 0, 12, 0, SXF_NOCHECKPOSITION | SXF_SETMASTER | SXF_ISMASTER);
			FROD CCCC 1 A_WeaponOffset(2, -3, WOF_ADD | WOF_INTERPOLATE);
			FROD C 4;
		Fishin:
			FROD D 1 A_Refire("ReelinStart");
			Loop;
		ReelinStart:
			FROD D 0 {if (master) {master.ACS_NamedExecute("returnhook", 0, 0, 0, 0);}}
			Goto Reelin;
		Reelin:
			FROD DDDDEEEEFFFF 1 
			{
				++invoker.fishtimer;
				A_WeaponOffset(random(-2, 2), random(-2, 2), WOF_INTERPOLATE);
				if (invoker.fishtimer > 100)
				{
					GiveInventory("DoneFishing", 1); //can also be given to player via ACS script to end animation early.
				}
			}
			FROD # 0 A_JumpIfInventory("DoneFishing", 1, "DoneFishin");
			Loop;
		DoneFishin:
			FROD GGGGGGGGGG 1 A_WeaponOffset(0, 24, WOF_ADD | WOF_INTERPOLATE);
			FROD AAAAAAAAAA 1 A_WeaponOffset(0, -24, WOF_ADD | WOF_INTERPOLATE);
			FROD # 2 
			{
				A_TakeInventory("DoneFishing", 99999);
				invoker.fishtimer = 0;
				ACS_NamedExecute("selffreezer", 0, 0, 0, 0);
			}
			Goto Ready;
		Spawn:
			FROD J -1;
			Stop;
	}
}

Class Fish : RandomSpawner
{
	Default
	{
		Radius 16;
		Height 16;
		DropItem "FishGold", 255, 10;
		DropItem "FishBig", 255, 2;
		DropItem "FishThin", 255, 2;
		DropItem "FishTrio", 255, 1;
		//$Category "Box Party/NPCs"
		//$Title "Fish (Random Spawner)"
		//$Sprite "FRODK0"
		//$Color 16
	}
}

Class FishGold : BoxActor
{
	Default
	{
		Health 10;
		Radius 16;
		Height 16;
		Speed 10;
		Species "Fish";
		+SOLID;
		+THRUSPECIES;
		+SHOOTABLE;
		+NOGRAVITY;
		+WALLSPRITE;
		+INTERPOLATEANGLES;
		+NOBLOOD;
	}

	override void Tick(void)
	{
		self.Angle += 90;
		Super.Tick();
		self.Angle -= 90;

		if ((waterlevel == 0) && (self.Health > 0)) {A_Die();}
	}
	
	States
	{
		Spawn:
			FROD K 10 A_Wander;
			Loop;
		Pain:
			FROD # 1 A_Die();
			Stop;
		Pain.FishingRod:
			//put stuff here
		Death.FishingRod:
			//put stuff here
		Death:
			TNT1 A 35 A_SpawnItemEX("SafeBoom", flags: SXF_NOCHECKPOSITION);
			Stop;
		
	}
}

Class FishBig : FishGold
{
	States
	{
		Spawn:
			FROD L 10 A_Wander;
			Loop;
	}
}

Class FishThin : FishGold
{
	States
	{
		Spawn:
			FROD M 10 A_Wander;
			Loop;
	}
}

Class FishTrio : FishGold
{
	States
	{
		Spawn:
			FROD N 10 A_Wander;
			Loop;
	}
}

Class DoneFishing : Inventory {Default {Inventory.MaxAmount 1;}} //

Class FishingHook : BoxActor
{
	Default
	{
	
	}
	
	override void Tick()
	{
		Super.Tick();
		
		if (self.waterlevel >= 1)
		{
			self.Vel.X = 0.0;
			self.Vel.Y = 0.0;
			if (self.Vel.Z < -2.0)
			{self.Vel.Z = -2.0;}
		}
	}
	
	void HookSpeedCap(double speedcap)
	{
		A_Face(master);
		let ZSpot = master.Pos.Z + (master.Height * master.Scale.Y * 0.5);
		double NewZSpot = (ZSpot + (self.pos.z * 4)) / 5;
		SetOrigin((self.pos.x, self.pos.y, NewZSpot), true);
		Thrust(1);
		if (self.Vel.X > speedcap) {self.Vel.X = speedcap;}
		if (self.Vel.Y > speedcap) {self.Vel.Y = speedcap;}
		if (self.Vel.X < -speedcap) {self.Vel.X = -speedcap;}
		if (self.Vel.Y < -speedcap) {self.Vel.Y = -speedcap;}
	}
	
	States
	{
		Spawn:
			FROD O 0 NoDelay A_AttachLightDef("glowing", "GLOWWHITE");
			FROD O -1;
			Loop;
		Returning:
			FROD O 1 
			{
				bNOGRAVITY = true;
				bNOCLIP = true;
				A_FadeOut(0.5);
				A_RemoveLight("glowing");
			}
			Goto ReturnLoop;
		ReturnLoop:
			FROD O 1 
			{
				if (master) 
				{
					HookSpeedCap(8);
					if (CheckProximity(master.GetClass(), 64) == true)	
					{
						master.GiveInventory("DoneFishing", 1);
						return ResolveState("Dying");
					}
					else {return ResolveState(null);}
				}
				else {return ResolveState("Dying");}
			}
			Loop;
		Dying:
			TNT1 A 20;
			Stop;
	}
}