//zboxscript/house/painting.zsc

CLASS Painting : BoxActor
{
	Default
	{
		Height 64;
		Radius 16;
		-SOLID;
		+NOINTERACTION;
		+WALLSPRITE;
		//$Category "Box Party/Decor/House/"
		//$Title "Painting"
		//$Color 2
	}
	
	States
	{
		Spawn:
			PANT Z 0 NoDelay A_Jump(16, "Rare");
			PANT Z 0 {frame = RandomPick(0, 6, 7, 8, 9, 10, 11, 12, 14, 23, 24);} //common paintings
			Goto Looper;
		Rare:
			PANT Z 1;
			PANT Z 0 A_Jump(16, "MoreRare");
			PANT Z 0 {frame = RandomPick(1, 2, 4, 5, 18, 21, 22);} //cats
			Goto Looper;
		MoreRare:
			PANT Z 1;
			PANT Z 0 A_Jump(128, "BigPainting");
			PANT Z 0 {frame = RandomPick(3, 13, 15, 16, 17, 19, 20);} //spooky
			Goto Looper;
		BigPainting:
			PANT Z 1 A_BoxScale(-0.5);
			PANB A 0 {frame = Random(0, 12);}
			Goto Looper;
		Looper:
			#### # 5
			{
				if (CheckRange(1500.0)) {A_FadeTo(0.0, 0.35, FTF_CLAMP);}
				else {A_FadeTo(1.0, 0.35, FTF_CLAMP);}
			}
			Loop;
	}
}