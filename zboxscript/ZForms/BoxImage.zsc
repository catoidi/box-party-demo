class boxparty_BoxImage : boxparty_Element {
	boxparty_BoxTextures boxTex;
	boxparty_BoxDrawer boxDrawer;
	Vector2 boxScale;

	void config(boxparty_BoxTextures textures = NULL, Vector2 scale = (1, 1)) {
		self.boxTex = textures;
		self.boxScale = scale;

		self.alpha = 1;
	}

	static boxparty_BoxImage create(Vector2 pos, Vector2 size, boxparty_BoxTextures textures = NULL, Vector2 scale = (1, 1)) {
		let ret = new('boxparty_BoxImage');

		ret.config(textures, scale);
		ret.setBox(pos, size);

		return ret;
	}

	override void drawer() {
		boxparty_AABB beforeClip, clipRect;
		screenClip(beforeClip, clipRect);
		Screen.setClipRect(int(clipRect.pos.x), int(clipRect.pos.y), int(clipRect.size.x), int(clipRect.size.y));

		boxDrawer.draw(self, (0, 0), box.size, boxTex, true, boxScale);

		Screen.setClipRect(int(beforeClip.pos.x), int(beforeClip.pos.y), int(beforeClip.size.x), int(beforeClip.size.y));
	}
}
