class boxparty_ZF_BoxImage : boxparty_ZF_Element {
	protected boxparty_ZF_BoxTextures boxTex;
	boxparty_ZF_BoxTextures getBoxTex() { return self.boxTex; }
	void setBoxTex(boxparty_ZF_BoxTextures boxTex) { self.boxTex = boxTex; }
	protected boxparty_ZF_BoxDrawer boxDrawer;

	protected Vector2 boxScale;
	Vector2 getBoxScale() { return self.boxScale; }
	void setBoxScale(Vector2 boxScale) { self.boxScale = boxScale; }

	void config(boxparty_ZF_BoxTextures textures = NULL, Vector2 scale = (1, 1)) {
		setBoxTex(textures);
		setBoxScale(scale);
		setAlpha(1.0);
	}

	static boxparty_ZF_BoxImage create(Vector2 pos, Vector2 size, boxparty_ZF_BoxTextures textures = NULL, Vector2 scale = (1, 1)) {
		let ret = new('boxparty_ZF_BoxImage');

		ret.config(textures, scale);
		ret.setBox(pos, size);

		return ret;
	}

	override void drawer() {
		if (hidden) { return; }

		boxparty_ZF_AABB beforeClip, clipRect;
		screenClip(beforeClip, clipRect);
		Screen.setClipRect(int(clipRect.pos.x), int(clipRect.pos.y), int(clipRect.size.x), int(clipRect.size.y));

		boxDrawer.draw(self, (0, 0), box.size, boxTex, true, boxScale);

		Screen.setClipRect(int(beforeClip.pos.x), int(beforeClip.pos.y), int(beforeClip.size.x), int(beforeClip.size.y));
	}
}
