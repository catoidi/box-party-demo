//zboxscript/enemies/bosspepper2.zsc

CLASS BossPepper2 : BoxMonster
{
	Default
	{
		Health 2400;
		Speed 16;
		Radius 72;
		Height 160;
		Mass 10000;
		RenderStyle "Translucent";
		Alpha 1.0;
		PainChance 6;
		PainChance "Melee", 64;
		PainChance "Lightning", 32;
		PainChance "PartyShot", 12;
		PainChance "Prism", 256;
		DamageFactor "Pepper", 0.0;
		DamageFactor "Melee", 2.0;
		DamageFactor "PartyShot", 1.25;
		BloodType "BigPepper2Blood";
		Tag "* GHOST *";
		Obituary "$BOX_DIE_BIGMELON";
		MinMissileChance 500;
		+BOSS;
		+NOBLOODDECALS;
		//$Category "Box Party/Enemies/"
		//$Title "Pepper (BOSS 2)"
		//$Color 4
	}
	
	bool isAngery;
	bool flashyTime;
	int startingHealth;
	int Exhaustion;
	int ExhaustMeter;
	
	action void A_BoxExhaust (int Amount)
	{
		if (invoker)
		{
			if (Amount == 0) {invoker.Exhaustion = 0;} //reset exhaust if amount is 0
			else
			{
				invoker.Exhaustion += Amount; //add exhaustion
				if (invoker.Exhaustion >= 100 + invoker.ExhaustMeter) //melon = exhausted
				{
					invoker.ExhaustMeter += 25; //harder to get exhausted now
					SetStateLabel("Exhausted"); //switch to exhaust state
				}
			}
		}
	}
	
	action void A_BossPepper2Charge (bool EvilMode = false) 
	{
		A_FaceTarget();
		if (EvilMode == false) {bSPRITEFLIP = false;}
		else 
		{
			bSPRITEFLIP = true;
			A_VileTarget("BossPepper2XHair");
		}
	}
	
	action void A_BossPepper2Attack (bool EvilMode = false) 
	{
		if (EvilMode == false)
		{
			A_SpawnProjectile("BossPepper2Bullet", 48, 0, 0, CMF_OFFSETPITCH, 0);
			bSPRITEFLIP = false;
		}
		else
		{
			A_SpawnProjectile("BossPepper2Bullet", 48, 0, 0, CMF_OFFSETPITCH, 0);
			A_SpawnProjectile("BossPepper2Bullet", 48, 0, 30, CMF_OFFSETPITCH, 0);
			A_SpawnProjectile("BossPepper2Bullet", 48, 0, -30, CMF_OFFSETPITCH, 0);
			bSPRITEFLIP = true;
		}
		A_StartSound("enemies/peppowA");
	}

	override void Tick(void)
	{
		Super.Tick();
		
		if (self)
		{
			if (startingHealth == 0) {startingHealth = self.Health;}
		
			if (isAngery == false) 
			{
				//currentBullet = "BigFruitBullet";
				A_RemoveLight("flashyred");
				A_FadeTo(1.0);
			}
			else 
			{
				//currentBullet = "BigFruitBullet2";
				if (random(0, 15) == 15) {A_SpawnItemEX("PlateSpawnerSpark", frandom(-32, 32), frandom(-32, 32), frandom(0, 64), frandom(-12, 12), frandom(-12, 12), frandom(0, 8), 0, SXF_NOCHECKPOSITION);}
				A_FadeTo(0.5);
			}
			
			if ((isAngery == true) && (flashyTime == false))
			{
				for (int countey = 1; countey < 16; countey++)
				{
					A_SpawnItemEX("PlateSpawnerSpark", frandom(-32, 32), frandom(-32, 32), frandom(0, 64), frandom(-12, 12), frandom(-12, 12), frandom(0, 8), 0, SXF_NOCHECKPOSITION);
				}
				A_AttachLightDef("flashyred", "GLOWREDGIANT");
				flashyTime = true;
				A_QuakeEX(4, 4, 4, 35, 0, 1280, 0);
				//bBRIGHT = true;
			}
			
			if ((self.Health <= (startingHealth / 2)) && (self.Health > 0)) {isAngery = true;}
			else if (self.Health < 1) {isAngery = false;} //angry until death
		}
	}
	
	States
	{
		Spawn:
			PEP2 OP 10 A_LookEx(label: "Standup");
			Loop;
		Standup:
			PEP2 OPA 10;
			Goto See;
		JustOneSec:
			#### # 0 A_ResetScale();
			PEP2 A 0
			{
				if (isAngery == true) {SetStateLabel("See");}
			}
			PEP2 A 0 A_BoxScaleXY(0.1, -0.1);
			PEP2 ABABA 5 Fast A_Chase(null, null);
			PEP2 A 0 A_BoxScaleXY(-0.1, 0.1);
			Goto See;
		See:
			PEP2 AABB 10 Fast A_Chase;
			Loop;
		Missile:
			#### # 0 {bNOPAIN = true;}
			#### # 0 A_ResetScale();
			#### # 0 {if (isAngery == true) {SetStateLabel("EVILMissile");}}
			#### # 0 A_StartSound("enemies/peploa1A", attenuation: ATTN_NONE);
			PEP2 CC 5 Slow A_BoxScaleXY(-0.05, 0.05);
			#### # 0 A_StartSound("enemies/peploa2A", attenuation: ATTN_NONE);
			PEP2 DD 5 Slow A_BoxScaleXY(0.05, -0.05);
			PEP2 DDD 3 BRIGHT Slow A_BossPepper2Charge();
			PEP2 E 4 BRIGHT Slow A_BossPepper2Attack();
			PEP2 D 2 Slow A_BoxScaleXY(-0.05, 0.05);
			PEP2 D 2 Slow A_BoxScaleXY(0.05, -0.05);
			PEP2 DDD 3 BRIGHT Slow A_BossPepper2Charge();
			PEP2 E 4 BRIGHT Slow A_BossPepper2Attack();
			PEP2 D 2 Slow A_BoxScaleXY(-0.05, 0.05);
			PEP2 D 2 Slow A_BoxScaleXY(0.05, -0.05);
			PEP2 DDD 3 BRIGHT Slow A_BossPepper2Charge();
			PEP2 E 4 BRIGHT Slow A_BossPepper2Attack();
			PEP2 D 2 Slow A_BoxScaleXY(-0.05, 0.05);
			PEP2 D 2 Slow A_BoxScaleXY(0.05, -0.05);
			PEP2 D 4 Slow A_BoxExhaust(10);
			#### # 0 {bNOPAIN = false;}
			Goto JustOneSec;
		EVILMissile:			
			#### # 0 A_StartSound("enemies/peploa1A", attenuation: ATTN_NONE);
			PEP2 CC 5 Slow A_BoxScaleXY(-0.05, 0.05);
			#### # 0 A_StartSound("enemies/peploa2A", attenuation: ATTN_NONE);
			PEP2 DD 5 Slow A_BoxScaleXY(0.05, -0.05);
		StartFiring:
			PEP2 DD 2 BRIGHT Slow A_BossPepper2Charge();
			PEP2 E 4 BRIGHT Slow A_BossPepper2Attack();
			PEP2 DD 2 BRIGHT Slow A_BossPepper2Charge();
			PEP2 E 4 BRIGHT Slow A_BossPepper2Attack();
			PEP2 DD 2 BRIGHT Slow A_BossPepper2Charge();
			PEP2 E 4 BRIGHT Slow A_BossPepper2Attack();
			#### # 0 A_StartSound("enemies/peploa2A", attenuation: ATTN_NONE);
			PEP2 DD 6 BRIGHT Slow A_BossPepper2Charge(true);
			PEP2 E 4 BRIGHT Slow A_BossPepper2Attack(true);
			PEP2 DD 2 BRIGHT Slow A_BossPepper2Charge();
			PEP2 E 4 BRIGHT Slow A_BossPepper2Attack();
			PEP2 DD 2 BRIGHT Slow A_BossPepper2Charge();
			PEP2 E 4 BRIGHT Slow A_BossPepper2Attack();
			PEP2 DD 2 BRIGHT Slow A_BossPepper2Charge();
			PEP2 E 4 BRIGHT Slow A_BossPepper2Attack();
			#### # 0 A_StartSound("enemies/peploa2A", attenuation: ATTN_NONE);
			PEP2 DD 6 BRIGHT Slow A_BossPepper2Charge(true);
			PEP2 E 4 BRIGHT Slow A_BossPepper2Attack(true);
			#### # 0 BRIGHT {bSPRITEFLIP = false;}
			PEP2 D 4 Slow A_BoxExhaust(20);
			#### # 0 A_Jump(32, "StartFiring");
			#### # 0 {bNOPAIN = false;}
			Goto JustOneSec;
		Pain.Lightning:
			PEP2 F 0 A_BoxShockedBig();
		Pain.Melee:
			#### # 0 {bNOPAIN = true;}
			PEP2 F 0 A_ResetScale();
			PEP2 F 0 ThrustThingZ(0, 3, 0, 1);
			PEP2 F 0 A_Pain;
			PEP2 F 1 A_BoxStars();
			PEP2 F 0 
			{
				if (IsPrism) {A_BoxDropLoot("Pure", -2, IsPrism);}
				else A_BoxDropLoot("Pure", -1);
			}
			#### # 0 {IsPrism = false;}
			PEP2 F 15 Slow A_FaceTarget;
			PEP2 F 0 A_BoxExhaust(16);
			#### # 0 {bNOPAIN = false;}
			Goto See;
		Pain:
			#### # 0 {bNOPAIN = true;}
			PEP2 F 0 A_ResetScale();
			PEP2 F 8 A_Pain;
			PEP2 F 0 A_BoxExhaust(8);
			#### # 0 {bNOPAIN = false;}
			Goto Missile;
		Death:
			PEP2 GJK 10;
			Stop;
		Death:
			PEP2 GGGGG 5 A_SpawnItemEX("SafeBoom", random(-64, 64), random(-64, 64), random(32, 96), random(-2, 2), random(-2, 2), random(-2, 2));
			PEP2 G 10;
			PEP2 J 0 A_BoxDropLoot("Pure", -4, IsPrism);
			PEP2 J 0 A_BoxDropAmmo(4);
			PEP2 J 0 A_SpawnItemEX("GIANTBOOM", random(-64, 64), random(-64, 64), random(32, 96), random(-2, 2), random(-2, 2), random(-2, 2));
			#### # 0 A_SpawnItemEX("BigPepper2Gib1", random(-64, 64), random(-64, 64), random(32, 96), random(-16, 16), random(-16, 16), random(2, 8));
			#### # 0 A_SpawnItemEX("BigPepper2Gib2", random(-64, 64), random(-64, 64), random(32, 96), random(-16, 16), random(-16, 16), random(2, 8));
			#### # 0 A_SpawnItemEX("BigPepper2Gib3", random(-64, 64), random(-64, 64), random(32, 96), random(-16, 16), random(-16, 16), random(2, 8));
			#### ################ 0 A_SpawnItemEx("BigPepper2Chunk2", 0, 0, 0, random(-24, 24), random(-24, 24), random(0, 18), random(0, 359), SXF_NOCHECKPOSITION);
			#### ############ 0 A_SpawnItemEX("BoomSmokeBig", random(-64, 64), random(-64, 64), random(32, 96), frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), 0, SXF_NOCHECKPOSITION);
			#### # 0 A_QuakeEX(3, 3, 3, 35, 0, 1280, 0);
			#### # 0 A_NoBlocking;
			PEP2 J 8;
			PEP2 K -1;
			Stop;
		Exhausted:
			PEP2 A 0 A_BoxDropLoot("Pure", -2, IsPrism);
			PEP2 A 0 A_BoxDropAmmo(2);
			PEP2 A 0 {bNOPAIN = true;}
			PEP2 OO 8 A_BoxScaleXY(-0.015, 0.015);
			PEP2 PP 8 A_BoxScaleXY(0.015, -0.015);
			PEP2 OO 8 A_BoxScaleXY(-0.015, 0.015);
			PEP2 PP 8 A_BoxScaleXY(0.015, -0.015);
			PEP2 OO 8 A_BoxScaleXY(-0.015, 0.015);
			PEP2 PP 8 A_BoxScaleXY(0.015, -0.015); //breathe in, breathe out
			PEP2 A 0 {bNOPAIN = false;}
			PEP2 A 0 A_BoxExhaust(0);
			Goto See;	
	}
}

//blood

CLASS BigPepper2Chunk : BoxChunk
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEP2 M 0 A_Jump(128, 2);
			PEP2 M 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS BigPepper2Chunk2 : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEP2 N 0 A_Jump(128, 2);
			PEP2 N 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS BigPepper2Blood : BoxBlood
{
	States
	{
		Firsty: 
			TNT1 AAA 0 A_SpawnItemEx("BigPepper2Chunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 208);
			TNT1 A 0 A_SpawnItemEx("BigPepper2Chunk2", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 224);
			Goto Super::Firsty;
		Secondy: 
			TNT1 AA 0 A_SpawnItemEx("BigPepper2Chunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 192);
			Goto Super::Secondy;
		Thirdy: 
			TNT1 A 0 A_SpawnItemEx("BigPepper2Chunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 192);
			Goto Super::Thirdy;
	}
}

CLASS BigPepper2Gib1 : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEP2 H 0 A_Jump(128, 2);
			"####" "#" 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
		Death.Sky:
		Death:
			"####" "#" 0 A_JumpIf((ACS_ScriptCall("CrumbCheck") >= 3), "Null");
			"####" "#" 0 {if (self.Pos.Z ~== self.FloorZ) {bFLATSPRITE = true;} else {bFLATSPRITE = false;}}
			"####" "#" -1;
			Stop;
	}
}

CLASS BigPepper2Gib2 : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEP2 I 0 A_Jump(128, 2);
			"####" "#" 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
		Death.Sky:
		Death:
			"####" "#" 0 A_JumpIf((ACS_ScriptCall("CrumbCheck") >= 3), "Null");
			"####" "#" 0 {if (self.Pos.Z ~== self.FloorZ) {bFLATSPRITE = true;} else {bFLATSPRITE = false;}}
			"####" "#" -1;
			Stop;
	}
}

CLASS BigPepper2Gib3 : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEP2 L 0 A_Jump(128, 2);
			"####" "#" 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
		Death.Sky:
		Death:
			"####" "#" 0 A_JumpIf((ACS_ScriptCall("CrumbCheck") >= 3), "Null");
			"####" "#" 0 {if (self.Pos.Z ~== self.FloorZ) {bFLATSPRITE = true;} else {bFLATSPRITE = false;}}
			"####" "#" -1;
			Stop;
	}
}

//projectile

CLASS BossPepper2Bullet : OnionBullet
{
	Default
	{
		Radius 15;
		Height 24;
		DamageFunction (10);
		Speed 16;
		+BRIGHT;
		//+MTHRUSPECIES;
	}

	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_AttachLightDef("bigbullet", "GLOWREDBIG");
			PEP2 Q 0 A_Jump(256, "SpeenLeft", "SpeenRight");
			Goto SpeenLeft;
		SpeenLeft:
			#### ## 1 
			{
				A_SetRoll(roll - 8);
				if (random(1, 2) == 2) {A_BoxParticle("ff eb 00", 1, 16, 70, PSpeed: 4.0, PBright: true);}
			}
			//#### # 0 A_SpawnProjectile("BossPepper2Bullet2", 0, 0, 0, CMF_OFFSETPITCH, 90);
			Loop;
		SpeenRight:
			#### ## 1 
			{
				A_SetRoll(roll + 8);
				if (random(1, 2) == 2) {A_BoxParticle("ff eb 00", 1, 16, 70, PSpeed: 4.0, PBright: true);}
			}
			//#### # 0 A_SpawnProjectile("BossPepper2Bullet2", 0, 0, 0, CMF_OFFSETPITCH, 90);
			Loop;
		Death.Sky:
		Death:
			TNT1 A 1 A_SpawnItemEx("DangerBoomPepper", 0, 0, 0, 0, 0, 0);
			Stop;
	}
}

CLASS BossPepper2Bullet2 : OnionBullet2
{
	Default 
	{
		Scale 0.4;
		Radius 3;
		Height 6;
		Speed 0;
		DamageFunction (3);
		//+MTHRUSPECIES;
	}
	
	States
	{
		Spawn:
			PEPR P 0 BRIGHT NoDelay A_Jump(256, "GoLeft", "GoRight");
			Goto GoLeft;
		GoLeft:
			PEPR P 1 BRIGHT {self.vel.z += -0.050;}
			Loop;
		GoRight:
			PEPR Q 1 BRIGHT {self.vel.z += -0.050;}
			Loop;
		Death.Sky:
		Death:
			PEPR # 1
			{
				A_BoxScale(0.1, true);
				A_FadeOut(0.2);
			}
			Loop;
	}
}

CLASS BossPepper2XHair : BoxActor
{
	Default
	{
		Gravity 0.0;
		RenderStyle "Translucent";
		Alpha 0.8;
		+BRIGHT;
	}
	
	States
	{
		Spawn:
			PEP2 R 1 A_AttachLightDef("glowtime", "GLOWRED");
			PEP2 R 1
			{
				A_BoxScale(0.1);
				A_FadeOut(0.16);
			}
			Loop;
	}
}