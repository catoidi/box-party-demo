//zboxscript/enemies/redonion.zsc

//based off onion
//look at onion.txt for parents and bullet code

CLASS RedOnionTurret : OnionTurret
{
	Default
	{
		Health 80;
		Radius 16;
		Height 32;
		BloodType "RedOnionBlood";
		MinMissileChance 1;
		Tag "Herbert";
		Obituary "$BOX_DIE_REDONION";
		//$Category "Box Party/Enemies/"
		//$Title "Onion (Small)"
		//$Color 4
	}
	
	action void A_BoxRedOnionShoot (void)
	{
		A_StartSound("enemies/onion1");
		A_SpawnProjectile("OnionBullet", 16, 0, 0);
		A_BoxParticle("00 ef ff", 4, 12, 30, PBright: true);
	}
	
	States
	{
		Spawn:
			ONI1 A 10 A_Look;
			Loop;
		See:
			ONI1 A 0 A_Jump(128, "Chill");
			Goto See2;
		See2:
			ONI1 AB 15 Fast A_Chase;
			Loop;
		Chill:
			ONI1 AB 15 Fast;
			Goto See2;
		Pain.Melee:
			#### # 0 A_ResetScale();
			ONI1 C 0 ThrustThingZ(0, 8, 0, 1);
			ONI1 C 0 A_Pain;
			ONI1 C 1 A_BoxStars();
			ONI1 C 0 A_BoxDropLoot("RedOnion", -1, IsPrism);
			#### # 0 {IsPrism = false;}
			ONI1 C 1 Slow A_SpawnItemEx("RedOnionChunk2", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), 0, 160);
			ONI1 C 8 Slow A_FaceTarget;
			Goto See;
		Pain:
			#### # 0 A_ResetScale();
			ONI1 C 0 A_Pain;
			Goto Missile;
		Missile:
			ONI1 C 0 A_RemoveLight("Shocking");
			ONI1 C 0 A_FaceTarget;
			ONI1 CC 3 Slow A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 0 A_BoxRedOnionShoot();
			ONI1 D 3 A_BoxScaleXY(0.15, -0.15);
			ONI1 C 3 A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 0 A_BoxRedOnionShoot();
			ONI1 D 3 A_BoxScaleXY(0.15, -0.15);
			ONI1 C 3 A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 0 A_BoxRedOnionShoot();
			ONI1 D 3 A_BoxScaleXY(0.15, -0.15);
			ONI1 CC 5 Fast A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 1 A_Jump(192, "Missile2");
			ONI1 D 0 A_ResetScale();
			Goto See;
		Missile2:
			ONI1 C 0 A_FaceTarget;
			ONI1 CC 5 Slow A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 0 A_BoxRedOnionShoot();
			ONI1 D 3 A_BoxScaleXY(0.15, -0.15);
			ONI1 C 3 A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 0 A_BoxRedOnionShoot();
			ONI1 D 3 A_BoxScaleXY(0.15, -0.15);
			ONI1 C 3 A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 0 A_BoxRedOnionShoot();
			ONI1 D 3 A_BoxScaleXY(0.15, -0.15);
			ONI1 CCC 5 Fast A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 1 A_Jump(128, "Missile3");
			ONI1 D 0 A_ResetScale();
			Goto See;
		Missile3:
			ONI1 C 0 A_FaceTarget;
			ONI1 CCC 5 Slow A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 0 A_BoxRedOnionShoot();
			ONI1 D 3 A_BoxScaleXY(0.15, -0.15);
			ONI1 C 3 A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 0 A_BoxRedOnionShoot();
			ONI1 D 3 A_BoxScaleXY(0.15, -0.15);
			ONI1 C 3 A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 0 A_BoxRedOnionShoot();
			ONI1 D 3 A_BoxScaleXY(0.15, -0.15);
			ONI1 CCCC 5 Fast A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 1 A_Jump(64, "Missile4");
			ONI1 D 0 A_ResetScale();
			Goto Chill;
		Missile4:
			ONI1 C 0 A_FaceTarget;
			ONI1 CCCC 5 Slow A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 0 A_BoxRedOnionShoot();
			ONI1 D 3 A_BoxScaleXY(0.15, -0.15);
			ONI1 C 3 A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 0 A_BoxRedOnionShoot();
			ONI1 D 3 A_BoxScaleXY(0.15, -0.15);
			ONI1 C 3 A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 0 A_BoxRedOnionShoot();
			ONI1 D 3 A_BoxScaleXY(0.15, -0.15);
			ONI1 CCCCC 5 Fast A_BoxScaleXY(-0.05, 0.05);
			ONI1 D 0 A_ResetScale();
			Goto Chill;
		Death.Massacre:
			#### # 0 A_ResetScale();
			ONI1 F 1 A_BoxSuperDie();
			#### # -1;
			Stop;
		Death.Melee:
			"####" "#" 0 {IsMelee = 1;}
			Goto Death;
		Death:
			#### # 0 A_ResetScale();
			ONI1 E 0 A_RemoveLight("Shocking");
			ONI1 E 10 A_FaceTarget;
			ONI1 F 1 A_BoxScream;
			ONI1 F 9 {
				A_SpawnItemEX("SafeBoomSmall", random(-2, 2), random(-2, 2), random(26, 38), random(-2, 2), random(-2, 2), random(-2, 2));
				A_SpawnItemEx("RedOnionChunk", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), 0, 64);
				A_SpawnItemEx("RedOnionChunk", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), 0, 64);
				A_SpawnItemEx("RedOnionChunk", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), 0, 128);
				A_SpawnItemEx("RedOnionChunk", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), 0, 128);
				A_SpawnItemEx("RedOnionChunk", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), 0, 192);
				A_SpawnItemEx("RedOnionChunk", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), 0, 192);
				A_SpawnItemEx("RedOnionChunk2", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), 0, 128);
				A_SpawnItemEx("RedOnionChunk2", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), 0, 128);
				A_SpawnItemEx("RedOnionChunk3", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359));
				A_BoxParticle("00 ef ff", 12, 12, 30, PBright: true);
				A_BoxDropLoot("RedOnion", 1, IsPrism);
			}
			ONI1 F 10 A_NoBlocking;
			ONI1 F -1;
			Stop;
		//Stun Effect Start
		Pain.Lightning:
			#### # 0 A_ResetScale();
			ONI1 C 2 BRIGHT {bNOPAIN = true;}
			#### # 0 BRIGHT A_AttachLightDef("Shocking", "SHOCKER");
			Goto ShockGo;
		ShockGo:
			#### ########## 10 BRIGHT A_BoxShocked();
			#### # 0 BRIGHT {bNOPAIN = false;}
			#### # 0 A_RemoveLight("Shocking");
			Goto See;
		//Stun Effect End
		//Prism Effect Start
		Pain.Prism:
			"####" "#" 0 {IsPrism = true;}
			Goto Pain.Melee;
		Death.Prism:
			"####" "#" 0 {IsPrism = true;}
			Goto Death.Melee;
		//Prism Effect End
	}
}

CLASS RedOnionBlood : BoxBlood
{
	States
	{
		Firsty: 
			TNT1 AAA 0 A_SpawnItemEx("RedOnionChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 64);
			TNT1 A 0 A_SpawnItemEx("RedOnionChunk2", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 128);
			TNT1 A 0 A_BoxParticle("00 ef ff", 1, 8, 20, PBright: true);
			Goto Super::Firsty;
		Secondy: 
			TNT1 AA 0 A_SpawnItemEx("RedOnionChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION);
			Goto Super::Secondy;
		Thirdy: 
			TNT1 A 0 A_SpawnItemEx("RedOnionChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION);
			Goto Super::Thirdy;
	}
}

CLASS RedOnionChunk : BoxChunk
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			ONI1 I 0 A_Jump(128, 2);
			ONI1 I 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS RedOnionChunk2 : BoxChunk
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			ONI1 H 0 A_Jump(128, 2);
			ONI1 H 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS RedOnionChunk3 : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			ONI1 G 0 A_Jump(128, 2);
			ONI1 G 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}