//zboxscript/enemies/bosspepper1.zsc

CLASS BossPepper1 : BoxMonster
{
	Default
	{
		Health 2400;
		Speed 44;
		Radius 80;
		Height 140;
		Mass 10000;
		PainChance 6;
		PainChance "Melee", 64;
		PainChance "Lightning", 32;
		PainChance "PartyShot", 12;
		PainChance "Prism", 256;
		DamageFactor "Pepper", 0.0;
		DamageFactor "Melee", 2.0;
		DamageFactor "PartyShot", 1.25;
		BloodType "BigPepper1Chunk";
		Species "Pepper";
		Tag "* BELL *";
		Obituary "$BOX_DIE_BIGPEPPER1";
		MaxTargetRange 16;
		+BOSS;
        +NOBLOODDECALS;
		+FLOORCLIP;
		+NOBLOODDECALS;
		+NOINFIGHTING;
		+DONTRIP;
		//$Category "Box Party/Enemies/"
		//$Title "Pepper (BOSS 1)"
		//$Color 4
	}
	
	bool isAngery;
	bool flashyTime;
	int startingHealth;
	int Exhaustion;
	int ExhaustMeter;
	int SweatTimer;
	
	action void A_BoxExhaust (int Amount)
	{
		if (invoker)
		{
			if (Amount == 0) {invoker.Exhaustion = 0;} //reset exhaust if amount is 0
			else
			{
				invoker.Exhaustion += Amount; //add exhaustion
				if (invoker.Exhaustion >= 100 + invoker.ExhaustMeter) //melon = exhausted
				{
					invoker.ExhaustMeter += 25; //harder to get exhausted now
					SetStateLabel("Exhausted"); //switch to exhaust state
				}
			}
		}
	}
	
	action void A_BossPepper1Charge (bool EvilMode = false)
	{
		if (EvilMode == true) {A_Chase(null, null);}
		A_FaceTarget();
	}
	
	action void A_BossPepper1Attack (bool EvilMode = false)
	{
		if (EvilMode == true) 
		{
			A_Chase(null, null);
		}
		A_SpawnProjectile("BossPepper1Bullet", 48, 0, 0, CMF_OFFSETPITCH, 0);
		A_StartSound("enemies/peppowB");
	}

	override void Tick(void)
	{
		Super.Tick();
		
		if (self)
		{
			if (startingHealth == 0) {startingHealth = self.Health;}
		
			if (isAngery == false) 
			{
				//currentBullet = "BigFruitBullet";
				A_RemoveLight("flashyred");
			}
			else 
			{
				//currentBullet = "BigFruitBullet2";
				if (random(0, 15) == 15) {A_SpawnItemEX("PlateSpawnerSpark", frandom(-32, 32), frandom(-32, 32), frandom(0, 64), frandom(-12, 12), frandom(-12, 12), frandom(0, 8), 0, SXF_NOCHECKPOSITION);}
			}
			
			if ((isAngery == true) && (flashyTime == false))
			{
				for (int countey = 1; countey < 16; countey++)
				{
					A_SpawnItemEX("PlateSpawnerSpark", frandom(-32, 32), frandom(-32, 32), frandom(0, 64), frandom(-12, 12), frandom(-12, 12), frandom(0, 8), 0, SXF_NOCHECKPOSITION);
				}
				A_AttachLightDef("flashyred", "GLOWREDGIANT");
				flashyTime = true;
				A_QuakeEX(4, 4, 4, 35, 0, 1280, 0);
				//bBRIGHT = true;
			}
			
			if ((self.Health <= (startingHealth / 2)) && (self.Health > 0)) {isAngery = true;}
			else if (self.Health < 1) {isAngery = false;} //angry until death
			
			//this is fucking awful
			int exhaustinv = CountInv("ExhaustionChecker");
			int exhaustmaxinv = CountInv("ExhaustionCheckerMax");
			int ActualExhaustMeter = ExhaustMeter + 100;
			
			if (exhaustinv < Exhaustion) {GiveInventory("ExhaustionChecker", Exhaustion - exhaustinv);}
			else if (exhaustinv > Exhaustion) {TakeInventory("ExhaustionChecker", Exhaustion - exhaustinv);}
			
			if (exhaustmaxinv < ActualExhaustMeter) {GiveInventory("ExhaustionCheckerMax", ActualExhaustMeter - exhaustmaxinv);}
			else if (exhaustmaxinv > ActualExhaustMeter) {TakeInventory("ExhaustionCheckerMax", ActualExhaustMeter - exhaustmaxinv);}
			//im sorry to anyone who sees this code
			
			if (SweatTimer < 16) {++SweatTimer;}
			else if (self.Health > 1)
			{
				if (exhaustinv >= (ActualExhaustMeter - 25)) {A_BoxStars("BossSweat", 3);}
				else if (exhaustinv >= (ActualExhaustMeter - 50)) {A_BoxStars("BossSweat", 1);}
				SweatTimer = 0;
			}
		}
	}
	
	States
	{
		Spawn:
			PEP1 RS 10 A_LookEx(label: "Standup");
			Loop;
		Standup:
			PEP1 RSA 10;
			Goto See;
		JustOneSec:
			#### # 0 A_ResetScale();
			PEP1 A 0
			{
				if (isAngery == true) {SetStateLabel("See");}
			}
			PEP1 A 0 A_BoxScaleXY(0.1, -0.1);
			PEP1 AA 25 Fast;
			PEP1 A 0 A_BoxScaleXY(-0.1, 0.1);
			Goto See;
		See:
			PEP1 AABB 4 Fast A_Chase;
			Loop;
		Missile:
			#### # 0 {bNOPAIN = true;}
			#### # 0 A_ResetScale();
			#### # 0 {if (isAngery == true) {SetStateLabel("EVILMissile");}}
			#### # 0 A_StartSound("enemies/peploa1B", attenuation: ATTN_NONE);
			PEP1 CCC 5 Slow 
			{
				A_BossPepper1Charge();
				A_BoxScaleXY(-0.05, 0.05);
			}
			#### # 0 A_StartSound("enemies/peploa2B", attenuation: ATTN_NONE);
			PEP1 DDD 5 Slow 
			{
				A_BossPepper1Charge();
				A_BoxScaleXY(0.05, -0.05);
			}
			PEP1 E 2 BRIGHT Slow A_BossPepper1Attack();
			PEP1 D 2 BRIGHT Slow A_BossPepper1Charge();
			PEP1 E 2 BRIGHT Slow A_BossPepper1Attack();
			PEP1 D 2 BRIGHT Slow A_BossPepper1Charge();
			PEP1 E 2 BRIGHT Slow A_BossPepper1Attack();
			PEP1 D 2 BRIGHT Slow A_BossPepper1Charge();
			PEP1 E 2 BRIGHT Slow A_BossPepper1Attack();
			PEP1 D 2 BRIGHT Slow A_BossPepper1Charge();
			PEP1 E 2 BRIGHT Slow A_BossPepper1Attack();
			PEP1 D 2 BRIGHT Slow A_BossPepper1Charge();
			PEP1 E 2 BRIGHT Slow A_BossPepper1Attack();
			PEP1 D 4 Slow A_BoxExhaust(8);
			#### # 0 {bNOPAIN = false;}
			Goto JustOneSec;
		EVILMissile:
			#### # 0 {self.Speed = 22;}
			#### # 0 A_StartSound("enemies/peploa1B", attenuation: ATTN_NONE);
			PEP1 CCCC 5 Slow 
			{
				A_BossPepper1Charge();
				A_BoxScaleXY(-0.05, 0.05);
			}
			#### # 0 A_StartSound("enemies/peploa2B", attenuation: ATTN_NONE);
			PEP1 DDDD 5 Slow 
			{
				A_BossPepper1Charge();
				A_BoxScaleXY(0.05, -0.05);
			}
		StartFiring:
			PEP1 E 2 BRIGHT Slow A_BossPepper1Attack(true);
			PEP1 F 2 BRIGHT Slow A_BossPepper1Charge(true);
			PEP1 G 2 BRIGHT Slow A_BossPepper1Attack(true);
			PEP1 D 2 BRIGHT Slow A_BossPepper1Charge(true);
			PEP1 E 2 BRIGHT Slow A_BossPepper1Attack(true);
			PEP1 F 2 BRIGHT Slow A_BossPepper1Charge(true);
			PEP1 G 2 BRIGHT Slow A_BossPepper1Attack(true);
			PEP1 D 2 BRIGHT Slow A_BossPepper1Charge(true);
			PEP1 E 2 BRIGHT Slow A_BossPepper1Attack(true);
			PEP1 F 2 BRIGHT Slow A_BossPepper1Charge(true);
			PEP1 G 2 BRIGHT Slow A_BossPepper1Attack(true);
			#### # 0 {self.Speed = 44;}
			PEP1 D 4 Slow A_BoxExhaust(16);
			#### # 0 A_Jump(100, "StartFiring");
			#### # 0 {bNOPAIN = false;}
			Goto JustOneSec;
		Pain.Lightning:
			PEP1 H 0 A_BoxShockedBig();
		Pain.Melee:
			#### # 0 {bNOPAIN = true;}
			PEP1 H 0 A_ResetScale();
			PEP1 H 0 ThrustThingZ(0, 3, 0, 1);
			PEP1 H 0 A_Pain;
			PEP1 H 1 A_BoxStars();
			PEP1 H 0 
			{
				if (IsPrism) {A_BoxDropLoot("Pure", -2, IsPrism);}
				else A_BoxDropLoot("Pure", -1);
			}
			#### # 0 {IsPrism = false;}
			PEP1 H 15 Slow A_FaceTarget;
			PEP1 H 0 A_BoxExhaust(16);
			#### # 0 {bNOPAIN = false;}
			Goto See;
		Pain:
			#### # 0 {bNOPAIN = true;}
			PEP1 H 0 A_ResetScale();
			PEP1 H 8 A_Pain;
			PEP1 H 0 A_BoxExhaust(8);
			#### # 0 {bNOPAIN = false;}
			Goto Missile;
		Death:
			PEP1 IIIII 5 A_SpawnItemEX("SafeBoom", random(-64, 64), random(-64, 64), random(32, 96), random(-2, 2), random(-2, 2), random(-2, 2));
			PEP1 I 10;
			TNT1 A 0 A_BoxDropLoot("Pure", -4, IsPrism);
			//TNT1 A 0 A_BoxDropAmmo(4);
			TNT1 A 0 A_SpawnItemEX("GIANTBOOM", random(-64, 64), random(-64, 64), random(32, 96), random(-2, 2), random(-2, 2), random(-2, 2));
			#### # 0 A_SpawnItemEX("BigPepper1Gib1", random(-64, 64), random(-64, 64), random(32, 96), random(-16, 16), random(-16, 16), random(2, 8));
			#### # 0 A_SpawnItemEX("BigPepper1Gib2", random(-64, 64), random(-64, 64), random(32, 96), random(-16, 16), random(-16, 16), random(2, 8));
			#### # 0 A_SpawnItemEX("BigPepper1Gib3", random(-64, 64), random(-64, 64), random(32, 96), random(-16, 16), random(-16, 16), random(2, 8));
			#### # 0 A_SpawnItemEX("BigPepper1Gib4", random(-64, 64), random(-64, 64), random(32, 96), random(-16, 16), random(-16, 16), random(2, 8));
			#### # 0 A_SpawnItemEX("BigPepper1Gib5", random(-64, 64), random(-64, 64), random(32, 96), random(-16, 16), random(-16, 16), random(2, 8));
			#### # 0 A_SpawnItemEX("BigPepper1Gib6", random(-64, 64), random(-64, 64), random(32, 96), random(-16, 16), random(-16, 16), random(2, 8));
			#### ################ 0 A_SpawnItemEx("BigPepper1Chunk2", 0, 0, 0, random(-24, 24), random(-24, 24), random(0, 18), random(0, 359), SXF_NOCHECKPOSITION);
			#### ############ 0 A_SpawnItemEX("BoomSmokeBig", random(-64, 64), random(-64, 64), random(32, 96), frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), 0, SXF_NOCHECKPOSITION);
			#### # 0 A_QuakeEX(3, 3, 3, 35, 0, 1280, 0);
			#### # 0 A_NoBlocking;
			TNT1 A -1;
			Stop;
		Exhausted:
			PEP1 A 0 A_BoxDropLoot("Pure", -2, IsPrism);
			PEP1 A 0 A_BoxDropAmmo(2);
			PEP1 A 0 {bNOPAIN = true;}
			PEP1 RR 8 A_BoxScaleXY(-0.015, 0.015);
			PEP1 SS 8 A_BoxScaleXY(0.015, -0.015);
			PEP1 RR 8 A_BoxScaleXY(-0.015, 0.015);
			PEP1 SS 8 A_BoxScaleXY(0.015, -0.015);
			PEP1 RR 8 A_BoxScaleXY(-0.015, 0.015);
			PEP1 SS 8 A_BoxScaleXY(0.015, -0.015); //breathe in, breathe out
			PEP1 A 0 {bNOPAIN = false;}
			PEP1 A 0 A_BoxExhaust(0);
			Goto See;	
	}
}

CLASS BossPepper1Bullet : BoxActor
{
	Default
	{
		Mass 0;
		Gravity 0;
		Radius 20;
		Height 32;
		DamageFunction (3);
		DamageType "Pepper";
		Speed 24;
		RenderStyle "Translucent";
		Alpha 0.8;
		Projectile;
		+RIPPER;
		+BLOODSPLATTER;
		+FORCEXYBILLBOARD;
		//+MTHRUSPECIES;
	}

	States
	{
		Spawn:
			PEP1 T 0 BRIGHT NoDelay A_AttachLightDef("glowtime", "GLOWAQUABIG");
			Goto Looper;
		Looper:
            PEP1 TU 4 BRIGHT 
			{
				
				A_BoxParticle("00 d7 ff", 1, 16, 70, POffset: 4.0, PSpeed: 3.0, PBright: true);
				A_BoxScale(0.1);
				A_FadeOut(0.1);
            }
			Loop;
		Death.Sky:
		Death:
			PEP1 U 1
			{
				A_BoxScale(0.1);
				A_FadeOut(0.1);
			}
			Loop;
	}
}

//blood

CLASS BigPepper1Chunk : BoxChunk
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEP1 P 0 A_Jump(128, 2);
			PEP1 P 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS BigPepper1Chunk2 : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEP1 Q 0 A_Jump(128, 2);
			PEP1 Q 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS BigPepper1Blood : BoxBlood
{
	States
	{
		Firsty: 
			TNT1 AAA 0 A_SpawnItemEx("BigPepper1Chunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 208);
			TNT1 A 0 A_SpawnItemEx("BigPepper1Chunk2", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 224);
			Goto Super::Firsty;
		Secondy: 
			TNT1 AA 0 A_SpawnItemEx("BigPepper1Chunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 192);
			Goto Super::Secondy;
		Thirdy: 
			TNT1 A 0 A_SpawnItemEx("BigPepper1Chunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 192);
			Goto Super::Thirdy;
	}
}

CLASS BigPepper1Gib1 : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEP1 J 0 A_Jump(128, 2);
			"####" "#" 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
		Death.Sky:
		Death:
			"####" "#" 0 A_JumpIf((ACS_ScriptCall("CrumbCheck") >= 3), "Null");
			"####" "#" 0 {if (self.Pos.Z ~== self.FloorZ) {bFLATSPRITE = true;} else {bFLATSPRITE = false;}}
			"####" "#" -1;
			Stop;
	}
}

CLASS BigPepper1Gib2 : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEP1 K 0 A_Jump(128, 2);
			"####" "#" 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
		Death.Sky:
		Death:
			"####" "#" 0 A_JumpIf((ACS_ScriptCall("CrumbCheck") >= 3), "Null");
			"####" "#" 0 {if (self.Pos.Z ~== self.FloorZ) {bFLATSPRITE = true;} else {bFLATSPRITE = false;}}
			"####" "#" -1;
			Stop;
	}
}

CLASS BigPepper1Gib3 : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEP1 L 0 A_Jump(128, 2);
			"####" "#" 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
		Death.Sky:
		Death:
			"####" "#" 0 A_JumpIf((ACS_ScriptCall("CrumbCheck") >= 3), "Null");
			"####" "#" 0 {if (self.Pos.Z ~== self.FloorZ) {bFLATSPRITE = true;} else {bFLATSPRITE = false;}}
			"####" "#" -1;
			Stop;
	}
}

CLASS BigPepper1Gib4 : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEP1 M 0 A_Jump(128, 2);
			"####" "#" 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
		Death.Sky:
		Death:
			"####" "#" 0 A_JumpIf((ACS_ScriptCall("CrumbCheck") >= 3), "Null");
			"####" "#" 0 {if (self.Pos.Z ~== self.FloorZ) {bFLATSPRITE = true;} else {bFLATSPRITE = false;}}
			"####" "#" -1;
			Stop;
	}
}

CLASS BigPepper1Gib5 : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEP1 N 0 A_Jump(128, 2);
			"####" "#" 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
		Death.Sky:
		Death:
			"####" "#" 0 A_JumpIf((ACS_ScriptCall("CrumbCheck") >= 3), "Null");
			"####" "#" 0 {if (self.Pos.Z ~== self.FloorZ) {bFLATSPRITE = true;} else {bFLATSPRITE = false;}}
			"####" "#" -1;
			Stop;
	}
}


CLASS BigPepper1Gib6 : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEP1 O 0 A_Jump(128, 2);
			"####" "#" 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
		Death.Sky:
		Death:
			"####" "#" 0 A_JumpIf((ACS_ScriptCall("CrumbCheck") >= 3), "Null");
			"####" "#" 0 {if (self.Pos.Z ~== self.FloorZ) {bFLATSPRITE = true;} else {bFLATSPRITE = false;}}
			"####" "#" -1;
			Stop;
	}
}