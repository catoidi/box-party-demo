//zboxscript/enemies/fruitpuncher.zsc

//its a watermelon that punches ya.

CLASS FruitPuncher : BoxMonster //20007
{
	Default
	{
		Speed 20;
		Health 80;
		Radius 16;
		Height 40;
		Mass 200;
		PainChance 128;
		PainChance "Melee", 256;
		PainChance "Lightning", 192;
		PainChance "PartyShot", 100;
		BloodType "FruitBlood";
		Species "Melon";
		Tag "Fruit Puncher";
		Obituary "$BOX_DIE_FRUIT";
		+FLOORCLIP;
		+JUMPDOWN;
		//$Category "Box Party/Enemies/"
		//$Title "Melon"
		//$Color 4
	}
	
	States
	{
		Spawn:
			MELN A 10 A_Look;
			Loop;
		See:
			MELN AABB 3 Fast A_Chase;
			Loop;
		Melee:
			#### C 0 A_FaceTarget;
			#### CC 5 Slow A_BoxScaleXY(-0.025, 0.05);
			#### D 0 A_Recoil(-10);
			#### D 0 A_CustomMeleeAttack(random(10, 20), "enemies/melee", "enemies/whiff", "Melee", false);
			#### D 10 A_BoxScaleXY(0.05, -0.1);
			Goto See;
		Pain.Melee:
			#### I 0 A_RemoveLight("Shocking");
			#### I 0 A_ResetScale();
			#### I 0 A_Pain;
			#### I 1 A_BoxStars();
			#### I 0 A_BoxDropLoot("Melon", -1, IsPrism);
			#### # 0 {IsPrism = false;}
			#### I 7 ThrustThingZ(0, 10, 0, 1);
			#### I 8;
			Goto See;
		Pain:
			#### I 0 A_RemoveLight("Shocking");
			#### I 0 A_ResetScale();
			#### I 0 A_Pain;
			#### I 4;
			#### III 0 A_SpawnItemEX("FruitGusher", random(0, 8), random(0, 8), random(24, 40), random(-4, 4), random(-4, 4), random(-4, 4), 0, SXF_ORIGINATOR, 128);
			#### I 4;
			Goto See;
		Death.Massacre:
			#### G 0 A_ResetScale();
			#### G 1 A_BoxSuperDie();
			#### # -1;
			Stop;
		Death.Melee:
			#### # 0 {IsMelee = 1;}
			Goto Death;
		Death:
			#### # 0 {if (random(1, 10000) == 10000) {SetStateLabel("FadeDeath");}}
			#### E 0 A_RemoveLight("Shocking");
			#### E 0 A_ResetScale();
			#### E 1 A_FaceTarget;
			#### E 1 A_Recoil(3);
			#### E 1 ThrustThingZ(0, 15, 0, 1);
			#### FFFFFFFFFFFFFFFFFFFFFF 0 A_SpawnItemEX("FruitGusher", random(0, 8), random(0, 8), random(24, 40), random(-20, 20), random(-20, 20), random(4, 20), random(0, 359), SXF_ORIGINATOR, 128);
			#### FFFFFFFFFFFF 0 A_SpawnItemEx("MelonChunk", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(4, 10), random(0, 359), 0, 200);
			#### F 0 A_SpawnItemEX("FruitGib_Torso", random(0, 8), random(0, 8), random(24, 40), random(-4, 4), random(-4, 4), random(0, 4), random(0, 359), SXF_ORIGINATOR | SXF_TRANSFERSCALE);
			#### F 0 A_SpawnItemEX("FruitGib_Glasses", random(0, 8), random(0, 8), random(24, 40), random(-20, 20), random(-20, 20), random(0, 20), random(0, 359), SXF_ORIGINATOR | SXF_TRANSFERSCALE);
			#### F 0 A_SpawnItemEX("FruitGib_LeftArm", random(0, 8), random(0, 8), random(24, 40), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), SXF_ORIGINATOR | SXF_TRANSFERSCALE);
			#### F 0 A_SpawnItemEX("FruitGib_RightArm", random(0, 8), random(0, 8), random(24, 40), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), SXF_ORIGINATOR | SXF_TRANSFERSCALE);
			#### F 0 A_SpawnItemEX("FruitGib_LeftLeg", random(0, 8), random(0, 8), random(24, 40), random(-12, 16), random(-12, 16), random(0, 16), random(0, 359), SXF_ORIGINATOR | SXF_TRANSFERSCALE);
			#### F 0 A_SpawnItemEX("FruitGib_RightLeg", random(0, 8), random(0, 8), random(24, 40), random(-12, 16), random(-12, 16), random(0, 16), random(0, 359), SXF_ORIGINATOR | SXF_TRANSFERSCALE);
			#### F 10 A_BoxScream;
			#### F 0 A_BoxDropLoot("Melon", 1, IsPrism);
			#### G 10 A_NoBlocking;
			#### G -1;
			Stop;
		FadeDeath:
			#### Q 0 A_RemoveLight("Shocking");
			#### # 0 A_ResetScale();
			#### # 0 A_NoBlocking;
			#### # 10 A_FaceTarget;
			#### #### 5 A_FadeOut(0.2);
			#### # 0 A_BoxDropLoot("Melon", 1, IsPrism);
			Stop;
		//Stun Effect Start
		Pain.Lightning:
			#### I 0 A_ResetScale();
			#### I 4 BRIGHT {bNOPAIN = true;}
			#### III 0 A_SpawnItemEX("FruitGusher", random(0, 8), random(0, 8), random(24, 40), random(-4, 4), random(-4, 4), random(-4, 4), 0, SXF_ORIGINATOR, 128);
			#### I 0 BRIGHT A_AttachLightDef("Shocking", "SHOCKER");
			Goto ShockGo;
		ShockGo:
			#### ########## 10 BRIGHT A_BoxShocked();
			#### # 0 BRIGHT {bNOPAIN = false;}
			#### # 0 A_RemoveLight("Shocking");
			Goto See;
		//Stun Effect End
		//Prism Effect Start
		Pain.Prism:
			#### # 0 {IsPrism = true;}
			Goto Pain.Melee;
		Death.Prism:
			#### # 0 {IsPrism = true;}
			Goto Death.Melee;
		//Prism Effect End
	}
}

CLASS FruitBlood : BoxBlood
{
	States
	{
		Firsty:
			TNT1 AAA 0 A_SpawnItemEX("FruitGusher", 0, 0, 0, random(-4, 4), random(-4, 4), random(-4, 4), 0, SXF_ORIGINATOR | SXF_NOCHECKPOSITION);
			TNT1 AAA 0 A_SpawnItemEx("MelonChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 100);
			TNT1 AAA 0 A_SpawnItemEx("FruitGusherPuff", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
			TNT1 A 0 A_BoxParticle("ff 00 00", 3, 12, 30);
			Goto Super::Firsty;
		Secondy:
			TNT1 AA 0 A_SpawnItemEX("FruitGusher", 0, 0, 0, random(-4, 4), random(-4, 4), random(-4, 4), 0, SXF_ORIGINATOR | SXF_NOCHECKPOSITION);
			TNT1 AA 0 A_SpawnItemEx("MelonChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 100);
			TNT1 AA 0 A_SpawnItemEx("FruitGusherPuff", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
			TNT1 A 0 A_BoxParticle("ff 00 00", 2, 12, 30);
			Goto Super::Secondy;
		Thirdy:
			TNT1 A 0 A_SpawnItemEX("FruitGusher", 0, 0, 0, random(-4, 4), random(-4, 4), random(-4, 4), 0, SXF_ORIGINATOR | SXF_NOCHECKPOSITION);
			TNT1 A 0 A_SpawnItemEx("MelonChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 100);
			TNT1 A 0 A_SpawnItemEx("FruitGusherPuff", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
			TNT1 A 0 A_BoxParticle("ff 00 00", 1, 12, 30);
			Goto Super::Thirdy;
	}
}

CLASS FruitGusher : BoxActor
{
	Default
	{
		Radius 8;
		Height 8;
		Mass 0;
	
		-SOLID;
		-BLOCKEDBYSOLIDACTORS;
		+MISSILE;
		+INTERPOLATEANGLES;
		+ROLLSPRITE;
		+SPRITEANGLE;
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			BLDR A 0 {frame = random(0, 3);}
			BLDR # 1 A_Jump(128, "SpeenLeft");
		SpeenRight:
			BLDR #################### 1 A_SetRoll(roll - 8);
			BLDR # 0 A_FadeOut(0.2);
			Loop;
		SpeenLeft:
			BLDR #################### 1 A_SetRoll(roll + 8);
			BLDR # 0 A_FadeOut(0.2);
			Loop;
		Death.Sky:
		Death:
			BLDR # 0;
			BLDR # 0 A_Jump(224, "DeathLoop");
			BLDR # 0 
			{if (self.Pos.Z ~== self.FloorZ) {A_SpawnItemEX("FruitGusherFlat", 0, 0, 0, 0, 0, 0, random(0, 359), SXF_NOCHECKPOSITION | SXF_TRANSFERSPRITEFRAME | SXF_TRANSFERSCALE);}}
			Goto DeathLoop;
		DeathLoop:
			BLDR # 1 A_BoxScale(0.2);
			BLDR # 0 A_FadeOut(0.2);
			Loop;
	}
}

CLASS FruitGusherPuff : ProjectileSmoke
{
	Default {Alpha 1.0;}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			BLDR A 0 {frame = random(0, 3);}
		Looper:
			BLDR # 1 A_FadeOut(0.05);
			#### # 0 A_BoxScale(0.05);
			Loop;
	}
}

CLASS FruitGusherFlat : BoxActor 
{
	Default
	{
		Radius 8;
		Height 8;
		Mass 0;
		Scale 0.2;
	
		-SOLID;
		-BLOCKEDBYSOLIDACTORS;
		+INTERPOLATEANGLES;
		+FLATSPRITE;
		+SPRITEANGLE;
	}
	
	States
	{
		Spawn:
			TNT1 A 0 A_SetRoll(random(0, 359)); //inherits sprite/frame from parent.
			#### # 0 A_BoxParticle("ff 00 00", 2, 12, 30);
			#### # 0 A_JumpIf((ACS_ScriptCall("CrumbCheck") >= 2), "Null");
			#### #################### 1 A_BoxScale(frandom(0.03, 0.05));
			#### # 900;
			Goto Looper;
		Looper:
			#### # 1 
			{
				A_BoxScale(-0.05);
				A_FadeOut(0.05);
			}
			Loop;
	}
}

CLASS MelonChunk : BoxChunk
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			MELN P 0 A_Jump(128, 2);
			MELN P 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}


CLASS FruitGib : BoxGib abstract
{
	States
	{
		SpeenRight:
			"####" "#" 1 A_SetRoll(roll - 3);
			"####" "#" 0 A_BoxFade(0.005);
			"####" "#" 0 A_SpawnItemEX("FruitGusher", random(0, 2), random(0, 2), random(0, 2), random(0, 1), random(0, 1), random(0, 1), 0, SXF_ORIGINATOR, 220);
			Loop;
		SpeenLeft:
			"####" "#" 1 A_SetRoll(roll - 3);
			"####" "#" 0 A_BoxFade(0.005);
			"####" "#" 0 A_SpawnItemEX("FruitGusher", random(0, 2), random(0, 2), random(0, 2), random(0, 1), random(0, 1), random(0, 1), 0, SXF_ORIGINATOR, 220);
			Loop;
	}
}

CLASS FruitGib_Torso : FruitGib
{
	States
	{
		Spawn:
			MELN J 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			MELN J 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS FruitGib_Glasses : FruitGib
{
	States
	{
		Spawn:
			MELN K 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			MELN K 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS FruitGib_LeftArm : FruitGib
{
	States
	{
		Spawn:
			MELN L 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			MELN L 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS FruitGib_RightArm : FruitGib
{
	States
	{
		Spawn:
			MELN M 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			MELN M 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS FruitGib_LeftLeg : FruitGib
{
	States
	{
		Spawn:
			MELN N 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			MELN N 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS FruitGib_RightLeg : FruitGib
{
	States
	{
		Spawn:
			MELN O 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			MELN O 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS FruitPuncher_Tiny : FruitPuncher
{
	Default 
	{
		Health 20;
		Radius 8;
		Height 20;
		Speed 5;
		Scale 0.5;
		DamageMultiply 0.25;
	}
}