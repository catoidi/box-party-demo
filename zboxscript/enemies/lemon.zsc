//zboxscript/enemies/lemon.zsc

CLASS Lemon : BoxMonster
{
	//Lemon is very similar to melon, in regards to pain and death animations.
	//So, lemon is gonna inherit some melon code.	

	Default
	{
		Speed 10;
		Health 120;
		Radius 16;
		Height 40;
		Mass 240;
		DamageFactor "Lemon", 1.75;
		DamageFactor "Melee", 1.5;
		DamageFactor "Lightning", 1.2;
		PainChance 160;
		PainChance "Lemon", 256;
		PainChance "Melee", 256;
		PainChance "Lightning", 224;
		PainChance "PartyShot", 104;
		BloodType "LemonBlood";
		BloodColor "Yellow";
		Species "Lemon";
		Tag "Kyle";
		Obituary "$BOX_DIE_LEMON";
		+FLOORCLIP;
		+NOINFIGHTSPECIES;
		//$Category "Box Party/Enemies/"
		//$Title "Lemon"
		//$Color 4
	}
	
	States
	{
		Spawn:
			LEMN A 10 A_Look;
			Loop;
		See:
			LEMN AABBBB 3 Fast A_Chase;
			Loop;
		Chill:
			LEMN AABBBB 3 Fast A_Chase(null, null);
			Goto See;
		Missile:
			LEMN DE 2 Slow;
			LEMN D 0 A_BoxScaleXY(-0.05, 0.05);
			LEMN DE 2 Slow;
			LEMN D 0 A_BoxScaleXY(-0.05, 0.05);
			LEMN DE 2 Slow;
			LEMN D 0 A_BoxScaleXY(-0.05, 0.05);
			LEMN DE 2 Slow;
			LEMN D 0 A_BoxScaleXY(-0.05, 0.05);
			LEMN DE 2 Slow;
			LEMN D 0 A_BoxScaleXY(-0.05, 0.05);
			LEMN F 2 A_FaceTarget;
			LEMN FFFFF 1 
			{
				A_StartSound("enemies/whiff", pitch: 1.5);
				A_SpawnProjectile("LemonBullet", frandom(24, 32), frandom(-4, 4), frandom(-2, 2), 0, frandom(8, 16));
				A_BoxScaleXY(0.1, -0.1);
			}
			LEMN DDDDD 2 Fast A_BoxScaleXY(-0.05, 0.05);
			LEMN D 0 A_Jump(128, "Chill");
			Goto See;
		Pain.Lemon:
		Pain.Melee:
			#### # 0 A_ResetScale();
			LEMN C 0 A_RemoveLight("Shocking");
			LEMN C 0 A_Pain;
			LEMN C 1 A_BoxStars();
			LEMN C 0 A_BoxDropLoot("Lemon", -1, IsPrism);
			#### # 0 {IsPrism = false;}
			LEMN C 7 ThrustThingZ(0, 10, 0, 1);
			LEMN CCC 0 A_SpawnItemEX("LemonGusher", random(0, 8), random(0, 8), random(24, 40), random(-4, 4), random(-4, 4), random(-4, 4), 0, SXF_ORIGINATOR, 128);
			LEMN C 8;
			Goto See;
		Pain:
			#### # 0 A_ResetScale();
			LEMN C 0 A_RemoveLight("Shocking");
			LEMN C 0 A_Pain;
			LEMN C 4;
			LEMN CCC 0 A_SpawnItemEX("LemonGusher", random(0, 8), random(0, 8), random(24, 40), random(-4, 4), random(-4, 4), random(-4, 4), 0, SXF_ORIGINATOR, 128);
			LEMN C 4;
			Goto See;
		Death.Massacre:
			#### # 0 A_ResetScale();
			TNT1 A 1 A_BoxSuperDie();
			#### # -1;
			Stop;
		Death.Melee:
			"####" "#" 0 {IsMelee = 1;}
			Goto Death;
		Death:
			#### # 0 A_ResetScale();
			LEMN G 0 A_RemoveLight("Shocking");
			LEMN G 1 A_FaceTarget;
			LEMN G 1 A_Recoil(3);
			LEMN G 1 ThrustThingZ(0, 15, 0, 1);
			LEMN GGGGGGGGGGGGGGGGGGGGGG 0 A_SpawnItemEX("LemonGusher", random(0, 8), random(0, 8), random(24, 40), random(-20, 20), random(-20, 20), random(4, 20), random(0, 359), SXF_ORIGINATOR, 128);
			LEMN GGGGGGGGGGGG 0 A_SpawnItemEx("LemonChunk", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(4, 10), random(0, 359), 0, 200);
			LEMN G 0 A_SpawnItemEX("LemonGib_LeftBody", random(0, 8), random(0, 8), random(24, 40), random(-4, 4), random(-4, 4), random(0, 4), random(0, 359), SXF_ORIGINATOR);
			LEMN G 0 A_SpawnItemEX("LemonGib_RightBody", random(0, 8), random(0, 8), random(24, 40), random(-4, 4), random(-4, 4), random(0, 4), random(0, 359), SXF_ORIGINATOR);
			LEMN G 0 A_SpawnItemEX("LemonGib_LeftFoot", random(0, 8), random(0, 8), random(24, 40), random(-12, 16), random(-12, 16), random(0, 16), random(0, 359), SXF_ORIGINATOR);
			LEMN G 0 A_SpawnItemEX("LemonGib_RightFoot", random(0, 8), random(0, 8), random(24, 40), random(-12, 16), random(-12, 16), random(0, 16), random(0, 359), SXF_ORIGINATOR);
			TNT1 A 10 A_BoxScream;
			TNT1 A 0 A_BoxDropLoot("Lemon", 1, IsPrism);
			TNT1 A 10 A_NoBlocking;
			TNT1 A -1;
			Stop;
		//Stun Effect Start
		Pain.Lightning:
			#### # 0 A_ResetScale();
			LEMN C 4 BRIGHT {bNOPAIN = true;}
			LEMN CCC 0 A_SpawnItemEX("LemonGusher", random(0, 8), random(0, 8), random(24, 40), random(-4, 4), random(-4, 4), random(-4, 4), 0, SXF_ORIGINATOR, 128);
			LEMN C 0 BRIGHT A_AttachLightDef("Shocking", "SHOCKER");
			Goto ShockGo;
		ShockGo:
			#### ########## 10 BRIGHT A_BoxShocked();
			#### # 0 BRIGHT {bNOPAIN = false;}
			#### # 0 A_RemoveLight("Shocking");
			Goto See;
		//Stun Effect End
		//Prism Effect Start
		Pain.Prism:
			"####" "#" 0 {IsPrism = true;}
			Goto Pain.Melee;
		Death.Prism:
			"####" "#" 0 {IsPrism = true;}
			Goto Death.Melee;
		//Prism Effect End
	}
}

CLASS DangerBoomLemon : DangerBoomSmall
{
	Default
	{
		DamageType "Lemon";
	}
}

CLASS LemonBullet : BoxActor
{
	Default
	{
		Mass 0;
		Gravity 0.3;
		Radius 4;
		Height 8;
		Speed 15;
		DamageFunction (frandom(3, 6));
		DamageType "Lemon";
		BounceType "Doom";
		BounceFactor 0.8;
		WallBounceFactor 0.25;
		BounceCount 2;
		Projectile;
		-NOGRAVITY;
		+BLOODSPLATTER;
		+INTERPOLATEANGLES;
		+ROLLSPRITE;
		+SPRITEANGLE;
		+FORCEXYBILLBOARD;
	}

	States
	{
		Spawn:
			LEMN N 0 BRIGHT NoDelay A_Jump(256, "SpeenLeft", "SpeenRight");
		SpeenLeft:
			"####" "#" 1 BRIGHT {
				A_SetRoll(roll - 8);
			}
			Loop;
		SpeenRight:
			"####" "#" 1 BRIGHT {
				A_SetRoll(roll + 8);
			}
			Loop;
		Death.Sky:
		Death:
			LEMN N 0 BRIGHT A_StartSound("traps/piebomb", volume: 0.3, pitch: 1.2);
			LEMN NNNO 2 BRIGHT;
			LEMN N 0 BRIGHT A_StartSound("traps/piebomb", volume: 0.3, pitch: 1.2);
			LEMN NNNO 2 BRIGHT;
			LEMN N 0 BRIGHT A_StartSound("traps/piebomb", volume: 0.3, pitch: 1.2);
			LEMN NNNO 2 BRIGHT;
			TNT1 A 0 A_SpawnItemEX("DangerBoomLemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS | SXF_NOCHECKPOSITION);
			TNT1 AA 0 A_SpawnItemEx("LemonChunk", random(-2, 2), random(-2, 2), random(2, 4), random(-6, 6), random(-6, 6), random(2, 8), random(0, 359), SXF_NOCHECKPOSITION, 128);
			TNT1 A 0 A_SpawnItemEx("LemonGusher", random(-2, 2), random(-2, 2), random(2, 4), random(-2, 2), random(-2, 2), random(1, 3), random(0, 359), SXF_NOCHECKPOSITION);
			TNT1 A 0 A_BoxParticle("f8 ff 86", 2, 12, 30);
			Stop;
	}
}

CLASS LemonBlood : BoxBlood
{
	States
	{
		Firsty:
			TNT1 AAA 0 A_SpawnItemEX("LemonGusher", 0, 0, 0, random(-4, 4), random(-4, 4), random(-4, 4), 0, SXF_ORIGINATOR | SXF_NOCHECKPOSITION);
			TNT1 AAA 0 A_SpawnItemEx("LemonChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 100);
			TNT1 AAA 0 A_SpawnItemEx("LemonGusherPuff", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
			TNT1 A 0 A_BoxParticle("f8 ff 86", 3, 12, 30);
			Goto Super::Firsty;
		Secondy:
			TNT1 AA 0 A_SpawnItemEX("LemonGusher", 0, 0, 0, random(-4, 4), random(-4, 4), random(-4, 4), 0, SXF_ORIGINATOR | SXF_NOCHECKPOSITION);
			TNT1 AA 0 A_SpawnItemEx("LemonChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 100);
			TNT1 AA 0 A_SpawnItemEx("LemonGusherPuff", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
			TNT1 A 0 A_BoxParticle("f8 ff 86", 2, 12, 30);
			Goto Super::Secondy;
		Thirdy:
			TNT1 A 0 A_SpawnItemEX("LemonGusher", 0, 0, 0, random(-4, 4), random(-4, 4), random(-4, 4), 0, SXF_ORIGINATOR | SXF_NOCHECKPOSITION);
			TNT1 A 0 A_SpawnItemEx("LemonChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 100);
			TNT1 A 0 A_SpawnItemEx("LemonGusherPuff", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
			TNT1 A 0 A_BoxParticle("f8 ff 86", 1, 12, 30);
			Goto Super::Thirdy;
	}
}

CLASS LemonGusher : FruitGusher
{
	Default
	{
		+DONTTRANSLATE;
	}

	States
	{
		Spawn:
			BLDY A 0 NoDelay A_SetRoll(random(0, 359));
			#### # 0 A_QueueCorpse;
			BLDY # 1 A_Jump(128, "SpeenLeft");
		SpeenRight:
			BLDY #################### 1 A_SetRoll(roll - 8);
			BLDY # 0 A_FadeOut(0.2);
			Loop;
		SpeenLeft:
			BLDY #################### 1 A_SetRoll(roll + 8);
			BLDY # 0 A_FadeOut(0.2);
			Loop;
		Death.Sky:
		Death:
			BLDY # 0;
			BLDY # 0 A_Jump(128, "DeathLoop");
			BLDY # 0 
			{if (self.Pos.Z ~== self.FloorZ) {A_SpawnItemEX("LemonGusherFlat", 0, 0, 0, 0, 0, 0, random(0, 359), SXF_NOCHECKPOSITION);}}
			Goto DeathLoop;
		DeathLoop:
			BLDY # 1 A_BoxScale(0.2);
			BLDY # 0 A_FadeOut(0.2);
			Loop;
	}
}

CLASS LemonGusherPuff : ProjectileSmoke
{
	Default {Alpha 1.0;}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
		Looper:
			BLDY A 1 A_FadeOut(0.05);
			#### # 0 A_BoxScale(0.05);
			Loop;
	}
}

CLASS LemonGusherFlat : FruitGusherFlat 
{
	Default
	{
		Radius 8;
		Height 8;
		Mass 0;
		Scale 0.2;
	
		-SOLID;
		-BLOCKEDBYSOLIDACTORS;
		+INTERPOLATEANGLES;
		+FLATSPRITE;
		+SPRITEANGLE;
	}
	
	States
	{
		Spawn:
			TNT1 A 0 A_SetRoll(random(0, 359));
			TNT1 A 0 A_BoxParticle("f8 ff 86", 2, 12, 30);
			#### # 0 A_QueueCorpse;
			TNT1 A 0 A_JumpIf((ACS_ScriptCall("CrumbCheck") >= 2), "Null");
			BLDY AAAAAAAAAAAAAAAAAAAA 1 A_BoxScale(frandom(0.03, 0.05));
			BLDY # 900;
			Goto Looper;
		Looper:
			BLDY # 1 
			{
				A_BoxScale(-0.05);
				A_FadeOut(0.05);
			}
			Loop;
	}
}

CLASS LemonChunk : BoxChunk
{
	States
	{	
		Spawn:
			LEMN M 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			LEMN M 0 A_Jump(128, 2);
			LEMN M 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS LemonGib : BoxGib abstract
{
	States
	{
		SpeenRight:
			"####" "#" 1 A_SetRoll(roll - 3);
			"####" "#" 0 A_BoxFade(0.005);
			"####" "#" 0 A_SpawnItemEX("LemonGusher", random(0, 2), random(0, 2), random(0, 2), random(0, 1), random(0, 1), random(0, 1), 0, SXF_ORIGINATOR, 220);
			Loop;
		SpeenLeft:
			"####" "#" 1 A_SetRoll(roll - 3);
			"####" "#" 0 A_BoxFade(0.005);
			"####" "#" 0 A_SpawnItemEX("LemonGusher", random(0, 2), random(0, 2), random(0, 2), random(0, 1), random(0, 1), random(0, 1), 0, SXF_ORIGINATOR, 220);
			Loop;
	}
}

CLASS LemonGib_LeftBody : LemonGib
{
	States
	{
		Spawn:
			LEMN H 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			LEMN H 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS LemonGib_RightBody : LemonGib
{
	States
	{
		Spawn:
			LEMN I 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			LEMN I 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS LemonGib_LeftFoot : LemonGib
{
	States
	{
		Spawn:
			LEMN J 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			LEMN J 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS LemonGib_RightFoot : LemonGib
{
	States
	{
		Spawn:
			LEMN K 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			LEMN K 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}