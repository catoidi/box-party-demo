//zboxscript/enemies/fruitpuncher.zsc

//its a watermelon that punches ya.

CLASS Pizzaulter : BoxMonster //20016
{
	Default
	{
		Speed 8;
		Health 160;
		Radius 24;
		Height 40;
		Mass 400;
		DamageFactor "Melee", 2;
		DamageFactor "Pizza", 1;
		PainChance 128;
		PainChance "Melee", 256;
		PainChance "Lightning", 192;
		PainChance "PartyShot", 128;
		BloodType "PizzaBlood";
		Species "Pizza";
		Tag "Pizzaulter";
		Obituary "$BOX_DIE_PIZZA_BIG";
		BoxMonster.CanSwim false;
		+FLOAT;
		+NOGRAVITY;
		+ROLLSPRITE;
		+ROLLCENTER;
		+INTERPOLATEANGLES;
		+NOBLOODDECALS;
		+FORCEXYBILLBOARD;
		-CASTSPRITESHADOW;
		//$Category "Box Party/Enemies/"
		//$Title "Pizza (Big)"
		//$Color 4
	}
	
	action void A_PizzaAttack (void)
	{
		A_SpawnProjectile("PepperoniBullet", random(30, 34), random(-4, 4), random(-6, 6), 0, random(-6, 6));
		A_SetRoll(roll - 40);
	}
	
	States
	{
		Spawn:
			PIZZ A 0 NoDelay A_SetRoll(random(0, 359));
			PIZZ A 10 A_Look;
			Loop;
		See:
			PIZZ A 0 A_SetRoll(roll + 8);
			PIZZ A 3 Fast A_Chase;
			Loop;
		Chill:
			PIZZ A 0 A_SetRoll(roll + 2);
			PIZZ A 3 Fast A_Chase(null, null);
			PIZZ A 0 A_Jump(32, "See");
			Loop;
		Missile:
			PIZZ G 10 Slow A_FaceTarget;
			PIZZ GGGGGG 1 BRIGHT A_PizzaAttack();
			PIZZ G 20;
			PIZZ G 0 A_Jump(96, "Chill");
			goto See;
		Pain.Melee:
			PIZZ A 0 A_RemoveLight("Shocking");
			PIZZ A 0 A_Pain;
			PIZZ A 1 A_BoxStars();
			PIZZ A 0 A_BoxDropLoot("Pizza", -2, IsPrism);
			#### # 0 {IsPrism = false;}
			PIZZ A 0 ThrustThingZ(0, 10, 0, 1);
			PIZZ A 39;
			Goto See;
		Pain:
			PIZZ A 0 A_RemoveLight("Shocking");
			PIZZ A 20 A_Pain;
			Goto See;
		Death.Massacre:
			TNT1 A 1 A_BoxSuperDie();
			#### # -1;
			Stop;
		Death.Melee:
			TNT1 A 0 A_RemoveLight("Shocking");
			TNT1 A 1 A_FaceTarget;
			TNT1 A 0 {
				A_BoxGib("PizzaGib", 6, 0, 128);
				A_BoxGib("PizzaChunk", 12, 1, 128);
				A_BoxDropLoot("Pizza", 3, IsPrism);
				A_SpawnItemEX("SafeBoom", random(-2, 2), random(-2, 2), random(22, 26), random(-2, 2), random(-2, 2), random(-2, 2));
				A_BoxParticle("cd 86 50", 9, 12, 20, POffset: 8.0);
			}
			Stop;
		Death:
			TNT1 A 0 A_RemoveLight("Shocking");
			TNT1 A 1 A_FaceTarget;
			TNT1 A 0 A_BoxScream;
			TNT1 A 0 {
				A_BoxGib("PizzaGib", 6, 0, 128);
				A_BoxGib("PizzaChunk", 12, 1, 128);
				A_BoxDropLoot("Pizza", 3, IsPrism);
				A_SpawnItemEX("SafeBoom", random(-2, 2), random(-2, 2), random(22, 26), random(-2, 2), random(-2, 2), random(-2, 2));
				A_BoxParticle("cd 86 50", 9, 12, 20, POffset: 8.0);
			}
			Stop;
		//Stun Effect Start
		Pain.Lightning:
			PIZZ A 2 BRIGHT {bNOPAIN = true;}
			#### # 0 BRIGHT A_AttachLightDef("Shocking", "SHOCKER");
			Goto ShockGo;
		ShockGo:
			#### ##### 10 BRIGHT A_BoxShocked();
			#### # 0 BRIGHT {bNOPAIN = false;}
			#### # 0 A_RemoveLight("Shocking");
			Goto See;
		//Stun Effect End
		//Prism Effect Start
		Pain.Prism:
			"####" "#" 0 {IsPrism = true;}
			Goto Pain.Melee;
		Death.Prism:
			"####" "#" 0 {IsPrism = true;}
			Goto Death.Melee;
		//Prism Effect End
	}
}

CLASS PizzaulterSmall : Pizzaulter //20017
{
	Default
	{
		Speed 6;
		Health 1;
		Radius 12;
		Height 20;
		Mass 60;
		PainChance 256;
		PainChance "Melee", 256;
		PainChance "Lightning", 256;
		PainChance "PartyShot", 256;
		DamageFunction (random(3, 5));
		Tag "Pizza Slicer";
		Obituary "$BOX_DIE_PIZZA_SMALL";
		-DONTHARMSPECIES;
		//$Category "Box Party/Enemies/"
		//$Title "Pizza (Small)"
		//$Color 4
	}
	
	States
	{
		Spawn:
			PIZZ B 0 NoDelay A_SetRoll(random(0, 359));
			#### # 0 A_AttachLightDef("hatered", "GLOWRED");
			PIZZ B 10 BRIGHT A_Look;
			Loop;
		See:
			PIZZ B 0 BRIGHT A_StopSound(CHAN_BODY);
			PIZZ B 0 BRIGHT A_SetRoll(roll + 2);
			PIZZ B 3 Fast BRIGHT A_Chase;
			Loop;
		Missile:
			PIZZ H 0 BRIGHT A_StopSound(CHAN_BODY);
			PIZZ H 10 Slow BRIGHT A_FaceTarget;
			PIZZ H 1 A_SkullAttack;
			PIZZ H 3 {self.Vel = ((self.Vel.X / 1.5), (self.Vel.Y / 1.5), (self.Vel.Z / 1.5));}
		Charger:
			PIZZ B 0 A_StartSound("enemies/pwoosh", CHAN_BODY, CHANF_NOSTOP);
			PIZZ B 0 A_BoxParticle("cd 86 50", 1, 24, 35, POffset: 2.0);
			PIZZ BBBB 1 A_SetRoll(roll - 12);
			Loop;
		Pain.Melee:
		Pain:
			PIZZ B 0 A_RemoveLight("Shocking");
			PIZZ B 10 A_Pain;
			Goto See;
		Death.Massacre:
			TNT1 A 1 A_BoxSuperDie();
			#### # -1;
			Stop;
		Death.Melee:
			"####" "#" 0 {IsMelee = 1;}
			Goto Death;
		Death:
			TNT1 A 0 A_RemoveLight("Shocking");
			TNT1 A 0 A_BoxScream;
			TNT1 A 1 {
				A_BoxGib("PizzaGib", 1, 0, 128);
				A_BoxGib("PizzaChunk", 2, 1, 128);
				A_BoxDropLoot("Pizza", 1, IsPrism);
				A_SpawnItemEX("SafeBoomSmall", random(-2, 2), random(-2, 2), random(22, 26), random(-2, 2), random(-2, 2), random(-2, 2));
				A_BoxParticle("cd 86 50", 3, 12, 20, POffset: 2.0);
			}
			Stop;
		//Stun Effect Start
		Pain.Lightning:
			PIZZ B 2 BRIGHT {bNOPAIN = true;}
			#### # 0 BRIGHT A_AttachLightDef("Shocking", "SHOCKER");
			Goto ShockGo;
		ShockGo:
			#### ########## 10 BRIGHT A_BoxShocked();
			#### # 0 BRIGHT {bNOPAIN = false;}
			#### # 0 A_RemoveLight("Shocking");
			Goto See;
		//Stun Effect End
		//Prism Effect Start
		Pain.Prism:
			"####" "#" 0 {IsPrism = true;}
			Goto Pain.Melee;
		Death.Prism:
			"####" "#" 0 {IsPrism = true;}
			Goto Death.Melee;
		//Prism Effect End
	}
}

CLASS PepperoniBullet : BoxActor
{
	Default
	{
		Mass 0;
		Gravity 0;
		Radius 4;
		Height 8;
		Speed 10;
		Scale 1.25;
		DamageFunction (random(3, 5));
		DamageType "Pizza";
		Projectile;
		-BLOODSPLATTER;
		+BLOODLESSIMPACT;
		+INTERPOLATEANGLES;
		+ROLLSPRITE;
		+SPRITEANGLE;
		+FORCEXYBILLBOARD;
	}
	
	States
	{
		Spawn:
			PIZZ C 0 BRIGHT A_Jump(256, "SpeenLeft", "SpeenRight");
		SpeenLeft:
			#### # 1 BRIGHT A_SetRoll(roll - 8);
			#### # 0 BRIGHT A_BoxParticle2("97 1d 00", randompick(0, 0, 0, 0, 0, 1), 4, 24, 0.0, 0.0, -0.25);
			Loop;
		SpeenRight:
			#### # 1 BRIGHT A_SetRoll(roll + 8);
			#### # 0 BRIGHT A_BoxParticle2("97 1d 00", randompick(0, 0, 0, 0, 0, 1), 4, 24, 0.0, 0.0, -0.25);
			Loop;
		Death.Sky:
		Death:
			TNT1 A 1 {
				A_SpawnItemEx("ProjectilePuffSmall", 0, 0, 0, 0, 0, 0);
				A_BoxParticle("97 1d 00", 3, 8, 32);
				A_SpawnItemEx("PepperoniBulletCrumb", random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 1), random(0, 359), 0, 200);
				A_SpawnItemEx("PepperoniBulletCrumb", random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 1), random(0, 359), 0, 200);
			}
			Stop;
	}
}

CLASS PepperoniBulletCrumb : BoxCrumb
{	
	States
	{
		Spawn:
			PIZZ D 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS PizzaCrumb : BoxCrumb
{
	States
	{	
		Spawn:
			PIZZ E 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PIZZ E 0 A_Jump(128, 2);
			PIZZ F 0;
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS PizzaChunk : BoxChunk
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PIZZ J 0 A_Jump(128, 2);
			PIZZ J 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS PizzaGib : BoxGib
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PIZZ I 0 A_Jump(128, 2);
			PIZZ I 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

CLASS PizzaBlood : BoxBlood
{
	States
	{
		Firsty: 
			TNT1 A 0 A_SpawnItemEx("PizzaChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION);
			TNT1 A 0 A_BoxParticle("cd 86 50", 3, 12, 20);
			Goto Super::Firsty;
		Secondy: 
			TNT1 AA 0 A_SpawnItemEx("PizzaCrumb", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 192);
			TNT1 A 0 A_BoxParticle("cd 86 50", 2, 12, 20);
			Goto Super::Secondy;
		Thirdy: 
			TNT1 A 0 A_SpawnItemEx("PizzaCrumb", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 192);
			TNT1 A 0 A_BoxParticle("cd 86 50", 1, 12, 20);
			Goto Super::Thirdy;
	}
}