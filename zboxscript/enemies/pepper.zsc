//zboxscript/enemies/pepper.zsc

class Pepper : BoxMonster
{
    Default
    {
        Speed 10;
        Radius 32;
        Height 64;
        Health 200;
        Mass 600;
		DamageFactor "Melee", 1.0;
		DamageFactor "PartyShot", 1.15;
        DamageFactor "Pepper", 0.25;
		PainChance 80;
		PainChance "Melee", 120;
		PainChance "Lightning", 60;
		PainChance "PartyShot", 50;
		PainChance "Popcorn", 30;
        PainChance "Pepper", 256;
        BloodType "PepperBlood";
		Species "Pepper";
        Tag "Jalape√±o";
		Obituary "$BOX_DIE_PEPPER";
        +NOBLOODDECALS;
		+FLOORCLIP;
		+JUMPDOWN;
		-DONTHARMSPECIES;
		//$Category "Box Party/Enemies/"
		//$Title "Pepper"
		//$Color 4
    }
	
	actor scriptor;

    States
    {
        Spawn:
			PEPR A 12 A_Look;
			Loop;
		See:
			PEPR AABB 10 Fast A_Chase;
			Loop;
		Chill:
			PEPR AABB 10 Fast A_Chase(null, null);
			Goto See;
        Missile:
			PEPR C 0 A_FaceTarget;
			PEPR C 0 A_StartSound("enemies/pepload1");
            PEPR CCCC 4 Slow A_BoxScaleXY(-0.0125, 0.025);
			PEPR D 0 A_FaceTarget;
			PEPR D 0 A_StartSound("enemies/pepload2");
            PEPR DDDD 4 Slow A_BoxScaleXY(0.0125, -0.025);
            PEPR D 0 A_FaceTarget;
			PEPR D 0 {if (target) {scriptor = target;}}
			PEPR E 0 BRIGHT A_StartSound("enemies/peppow");
            PEPR E 12 Fast BRIGHT A_SpawnProjectile("PepperBullet", 16, 0, 0);
            PEPR D 12 Fast A_FaceTarget;
			PEPR D 0 {if (target) {scriptor = target;}}
			PEPR E 0 BRIGHT A_StartSound("enemies/peppow");
            PEPR E 12 Fast BRIGHT A_SpawnProjectile("PepperBullet", 16, 0, 0);
            PEPR D 12 Fast;
			PEPR D 0 A_Jump(128, "Chill");
            Goto See;
        Pain.Melee:
			#### # 0 A_ResetScale();
			PEPR F 0 ThrustThingZ(0, 3, 0, 1);
			PEPR F 0 A_Pain;
			PEPR F 0 A_BoxStars();
			PEPR F 0 A_BoxDropLoot("Pepper", -1, IsPrism);
			#### # 0 {IsPrism = false;}
			PEPR F 12 Slow A_FaceTarget;
			Goto See;
        Pain:
			#### # 0 A_ResetScale();
			PEPR F 0 A_RemoveLight("Shocking");
			PEPR F 0 A_Pain;
			PEPR F 8;
			Goto See;
		Death.Pepper:
			#### # 0 {if (scriptor) {scriptor.ACS_NamedExecuteAlways("PepperUnlock", 0, 0, 0, 0);}} 
			Goto Death;
        Death.Melee:
			#### # 0 {IsMelee = 1;}
			Goto Death;
		Death:
			#### # 0 A_ResetScale();
			PEPR G 0 A_RemoveLight("Shocking");
			PEPR G 10 A_FaceTarget;
            PEPR H 0
            {
                A_SpawnItemEx("PepperGib1", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), SXF_NOCHECKPOSITION);
                A_SpawnItemEx("PepperGib2", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), SXF_NOCHECKPOSITION);
                A_SpawnItemEx("PepperGib3", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), SXF_NOCHECKPOSITION);
                A_SpawnItemEx("PepperGib4", random(-6, 6), random(-6, 6), random(26, 38), random(-12, 12), random(-12, 12), random(0, 12), random(0, 359), SXF_NOCHECKPOSITION);
                A_BoxParticle("ff fa bf", 12, 16, 35);
                A_BoxDropLoot("Pepper", 1, IsPrism);
            }
			TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("PepperChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 128);
			PEPR H 10 A_BoxScream;
			PEPR I 10 A_NoBlocking;
			PEPR I -1;
			Stop;
        //Stun Effect Start
		Pain.Lightning:
			#### # 0 A_ResetScale();
			PEPR F 4 BRIGHT {bNOPAIN = true;}
			PEPR F 0 BRIGHT A_AttachLightDef("Shocking", "SHOCKER");
			Goto ShockGo;
		ShockGo:
			#### ##### 10 BRIGHT A_BoxShocked();
			#### # 0 BRIGHT {bNOPAIN = false;}
			#### # 0 A_RemoveLight("Shocking");
			Goto See;
		//Stun Effect End
		//Prism Effect Start
		Pain.Prism:
			"####" "#" 0 {IsPrism = true;}
			Goto Pain.Melee;
		Death.Prism:
			"####" "#" 0 {IsPrism = true;}
			Goto Death.Melee;
		//Prism Effect End
    }
}

class PepperGib1 : BoxGib
{	
	States
	{
        Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEPR J 0 A_Jump(128, 2);
			PEPR J 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

class PepperGib2 : BoxGib
{	
	States
	{
        Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEPR K 0 A_Jump(128, 2);
			PEPR K 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

class PepperGib3 : BoxGib
{	
	States
	{
        Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEPR L 0 A_Jump(128, 2);
			PEPR L 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

class PepperGib4 : BoxGib
{	
	States
	{
        Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEPR M 0 A_Jump(128, 2);
			PEPR M 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

class PepperChunk : BoxChunk
{
	States
	{	
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
			"####" "#" 0 A_QueueCorpse;
			PEPR N 0 A_Jump(64, 2);
			PEPR O 0 {bSPRITEFLIP = true;}
			"####" "#" 1 A_Jump(128, "SpeenLeft");
			Goto SpeenRight;
	}
}

class PepperBlood : BoxBlood
{
	States
	{
		Firsty: 
			TNT1 AAA 0 A_SpawnItemEx("PepperChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 128);
            TNT1 A 0 A_BoxParticle("ff fa bf", 6);
			Goto Super::Firsty;
		Secondy: 
			TNT1 AA 0 A_SpawnItemEx("PepperChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 128);
            TNT1 A 0 A_BoxParticle("ff fa bf", 4);
			Goto Super::Secondy;
		Thirdy: 
			TNT1 A 0 A_SpawnItemEx("PepperChunk", 0, 0, 0, random(-6, 6), random(-6, 6), random(0, 6), random(0, 359), SXF_NOCHECKPOSITION, 128);
            TNT1 A 0 A_BoxParticle("ff fa bf", 2);
			Goto Super::Thirdy;
	}
}

class PepperBullet : OnionBullet
{
	Default
	{
		Radius 12;
		Height 24;
		DamageFunction (10);
		DamageType "Pepper";
		Speed 16;
		+BLOODSPLATTER;
	}

	States
	{
		Spawn:
            PEPR PQ 4 BRIGHT A_BoxParticle2("ff eb 00", 1, 16, 70, POffset: 4.0, POffsetZ: 4.0, PSpeed: -1.0, PBright: true);
            Loop;
		Death.Sky:
		Death:
			TNT1 A 1 A_SpawnItemEx("DangerBoomPepper", 0, 0, 0, 0, 0, 0);
			Stop;
	}
}

class DangerBoomPepper : DangerBoom {Default {DamageType "Pepper"; Obituary "$BOX_DIE_PEPPER";}}